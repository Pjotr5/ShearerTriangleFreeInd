import Mathlib
import ShearerTriangleFreeInd.Analysis
import ShearerTriangleFreeInd.Analysis_W

namespace SimpleGraph

open Finset SimpleGraph BigOperators
attribute [local instance] Classical.propDecidable

variable {V : Type} {G : SimpleGraph V} {v : V}
variable [Fintype V]


def VertexFinset (_G : SimpleGraph V) : Finset V := univ
noncomputable def averageDegree {V : Type} [Fintype V] (G : SimpleGraph V) := ğ”¼ v, (G.degree v : â„š)
noncomputable def indepSetFinsetAll (G : SimpleGraph V) : Finset (Set V) := {s | G.IsIndepSet s}

scoped notation "V(" G ")" => VertexFinset G
scoped notation "E(" G ")" => edgeFinset G
scoped notation "d(" G ")" => averageDegree G
scoped notation "Î±(" G ")" => indepNum G
scoped notation "â„(" G ")" => indepSetFinsetAll G

lemma VG_rw : V(G) = univ := rfl
lemma VG_Fintype : #V(G) = Fintype.card V := rfl

lemma d_rw : d(G) = ğ”¼ v, (G.degree v : â„š) := rfl

lemma I_rw {S : Set V} : S âˆˆ â„(G) â†” G.IsIndepSet S := by
  simp only [indepSetFinsetAll, mem_filter, mem_univ, true_and]


/-
  ## Lemmas about averageDegree
-/

lemma averageDegree_nonneg : 0 â‰¤ d(G) :=
  expect_nonneg (fun _ _ â†¦ Nat.cast_nonneg' _)

lemma averageDegree_pos_from_nonzero (hd : d(G) â‰  0) : 0 < d(G) :=
  lt_of_le_of_ne averageDegree_nonneg hd.symm

lemma averageDegree_eq_twice_card_edges_div_card : #V(G) * d(G) = 2 * #E(G) := by
  convert congrArg (Nat.cast (R := â„š)) (sum_degrees_eq_twice_card_edges G) using 1
  Â· by_cases hV : Nonempty V
    Â· rw [d_rw, Fintype.expect_eq_sum_div_card, VG_Fintype, (Nat.cast_sum univ _)]
      exact mul_div_cancelâ‚€ _ (by simp)
    Â· simp_all [VG_rw]
  Â· rw [Nat.cast_mul, Nat.cast_ofNat]

lemma edgeFinset_empty_from_averageDegree_zero (hd : d(G) = 0) : E(G) = âˆ… := by
  rw [â†card_eq_zero, â†Rat.natCast_eq_zero_iff, â†mul_eq_zero_iff_left (a := 2) (by norm_num),
      â†averageDegree_eq_twice_card_edges_div_card, hd, mul_zero]


lemma averageDegree_zero_from_vertexType_empty (hV : IsEmpty V) : d(G) = 0 := by
  simp only [averageDegree, univ_eq_empty, expect_empty]

lemma vertexType_Nonempty_from_averageDegree_pos (h : 0 < d(G)) : Nonempty V  := by
  by_contra hV
  rw [not_nonempty_iff] at hV
  linarith [averageDegree_zero_from_vertexType_empty (G := G) hV]


lemma edgeFinset_empty_iff_averageDegree_zero : E(G) = âˆ… â†” d(G) = 0 := by
  constructor
  Â· intro hE
    have this : #V(G) * d(G) = 0 := by
      simp [averageDegree_eq_twice_card_edges_div_card, card_eq_zero.mpr hE]
    rcases mul_eq_zero.1 this with (h | h)
    Â· refine averageDegree_zero_from_vertexType_empty (Fintype.card_eq_zero_iff.mp ?_ )
      exact_mod_cast h
    Â· exact h
  Â· exact edgeFinset_empty_from_averageDegree_zero


lemma averageDegree_real_cast : (d(G) : â„) = ğ”¼ v, (G.degree v : â„) :=
    algebraMap.coe_expect (M := â„š) _ _

/-
  ## Closed neighborhood of vertex
-/

omit [Fintype V] in
variable (G) (v) in
def closedNeighborSet := insert v (G.neighborSet v)

omit [Fintype V] in
lemma self_mem_closedNeighborSet : v âˆˆ G.closedNeighborSet v :=
  Set.mem_insert v (G.neighborSet v)

omit [Fintype V] in
lemma nonempty_closedNeighborSet : (G.closedNeighborSet v).Nonempty :=
    âŸ¨_, self_mem_closedNeighborSetâŸ©

omit [Fintype V] in
lemma Adj_of_mem_closedNeighborSet_of_ne_v {u v : V} (hne : v â‰  u)
    (hu : u âˆˆ G.closedNeighborSet v)
     : G.Adj v u := by
  rcases (Set.mem_insert_iff.1 hu) with (hu | hu)
  Â· exact (hne hu.symm).elim
  Â· exact hu

/-
  ## Lemmas induced graph vertex counts
-/

lemma induce_vertex_card (S : Set V) [DecidablePred (Â· âˆˆ S)]
    : #V(G.induce S) = #S.toFinset := by simp [VG_rw]

lemma vertex_card_induce_sum (S : Set V) : #V(G) = #S.toFinset + #Sá¶œ.toFinset := by
  convert (card_filter_add_card_filter_not (Â· âˆˆ S)).symm <;>
    (ext _; simp [VG_rw])

lemma induce_vertex_card_compl_lt {S : Set V} (hS : S.Nonempty) : #V(G.induce Sá¶œ) < #V(G) := by
  rw [induce_vertex_card, VG_rw]
  refine card_lt_card âŸ¨fun _ _ â†¦ mem_univ _, sdiff_nonempty.mp ?_âŸ©
  convert Set.Aesop.toFinset_nonempty_of_nonempty hS
  rw [Set.toFinset_compl, sdiff_compl, inf_of_le_right (le_iff_subset.2 (subset_univ _))]

lemma card_vertices_eq_zero_from_empty (hV : IsEmpty V) : #V(G) = 0 :=
  card_eq_zero.2 (univ_eq_empty_iff.2 hV)

omit [Fintype V] in
lemma vertex_singelton_card [Finite V] (v : V) : #(V(G.induce {v})) = 1 := by
  cases nonempty_fintype V
  convert induce_vertex_card {v}
  rw [Set.toFinset_singleton, card_singleton]

lemma closedNeighbor_card (v : V) : #(G.closedNeighborSet v).toFinset = G.degree v + 1 := by
  simp [closedNeighborSet, â†neighborFinset_eq_filter]

lemma expect_closedNeighbor_card_real (hV : Nonempty V)
    : ğ”¼ v, (#(G.closedNeighborSet v).toFinset : â„) = d(G) + 1 := by
  simp_rw [closedNeighbor_card, Nat.cast_add, expect_add_distrib, averageDegree_real_cast,
      Nat.cast_one, expect_const univ_nonempty]

lemma closedNeighbor_induce_card (v : V)
    : #(V(G.induce (G.closedNeighborSet v))) = G.degree v + 1 := by
  rw [â†closedNeighbor_card, induce_vertex_card]


/-
  ## Lemmas induced graph edge counts
-/

variable (G) in
noncomputable def EdgeFinsetInducedBy (S : Set V) := filter (fun e â†¦ âˆ€ v âˆˆ e, v âˆˆ S) E(G)

variable (G) in
noncomputable def EdgeIncidenceFinset (S : Set V) := filter (âˆƒ v âˆˆ S, v âˆˆ Â·) E(G)


lemma induceEdgeMap (S : Set V) :
    map (Function.Embedding.subtype _).sym2Map E(G.induce S)
    = G.EdgeFinsetInducedBy S := by
  ext e
  constructor <;> simp_rw [EdgeFinsetInducedBy, mem_map, mem_filter, mem_edgeFinset]
  Â· intro âŸ¨e', âŸ¨he', hee'âŸ©âŸ©; subst hee'
    refine âŸ¨(Embedding.map_mem_edgeSet_iff (Embedding.induce _)).2 he', ?_âŸ©
    simp only [Function.Embedding.sym2Map_apply, Function.Embedding.subtype_apply,
      Sym2.mem_map, Subtype.exists, exists_and_right, exists_eq_right]
    exact fun _ âŸ¨h, _âŸ© â†¦ h
  Â· simp only [Function.Embedding.sym2Map_apply, Function.Embedding.subtype_apply, and_imp]
    intro he hev
    have âŸ¨v, w, hvweâŸ©  := (Sym2.exists).1 âŸ¨e, rflâŸ©; subst hvwe
    refine âŸ¨s(âŸ¨v, hev _ (Sym2.mem_mk_left _ _)âŸ©, âŸ¨w, hev _ (Sym2.mem_mk_right _ _)âŸ©), he, by simpâŸ©

lemma induce_edge_card (S : Set V) :
    #E(G.induce S) = #(G.EdgeFinsetInducedBy S) := by rw [â†induceEdgeMap, card_map]


lemma EdgeIncidenceFinset_card_zero_from_averageDegree_zero
    (hd : d(G) = 0) (S : Set V) : #(G.EdgeIncidenceFinset S) = 0 := by
  simp [EdgeIncidenceFinset, edgeFinset_empty_from_averageDegree_zero hd]

lemma induceAveragedegree_zero_from_averageDegree_zero
    (hd : d(G) = 0) (S : Set V) : d(G.induce S) = 0 := by
  rw [â†edgeFinset_empty_iff_averageDegree_zero, â†card_eq_zero] at *
  rw [â†Nat.le_zero, â†hd, induce_edge_card]
  exact card_le_card (filter_subset _ _)

lemma edge_card_induce_sum (S : Set V) :
    #E(G) = #(G.EdgeIncidenceFinset S) + #E(induce Sá¶œ G) := by
  convert (card_filter_add_card_filter_not (âˆƒ v âˆˆ S, v âˆˆ Â·) (s := E(G))).symm
  convert (induce_edge_card Sá¶œ)
  ext _
  simp_rw [EdgeFinsetInducedBy, not_exists, not_and, Set.mem_compl_iff]
  congr! 3
  constructor <;> exact fun hx _ hv hvS â†¦ hx _ hvS hv

lemma edge_incidence_singelton_eq (v : V) : G.EdgeIncidenceFinset {v} = G.incidenceFinset v := by
  ext _
  simp [mem_incidenceFinset, EdgeIncidenceFinset, mem_filter, incidenceSet]

lemma edge_incidence_singelton_card (v : V) : #(G.EdgeIncidenceFinset {v}) = G.degree v := by
  rw [edge_incidence_singelton_eq, card_incidenceFinset_eq_degree]

lemma degree_zero_of_avg_zero (hd : d(G) = 0) : G.degree v = 0 := by
  rw [â†edge_incidence_singelton_card]
  exact EdgeIncidenceFinset_card_zero_from_averageDegree_zero hd _

lemma incident_closedNeighbor_mem_iff (e : Sym2 V) (v : V) :
    (e âˆˆ G.EdgeIncidenceFinset (G.closedNeighborSet v)) â†”
    âˆƒ w âˆˆ G.neighborSet v, e âˆˆ G.incidenceSet w := by
  simp_rw [EdgeIncidenceFinset, mem_filter, closedNeighborSet, Set.mem_insert_iff,
      mem_neighborSet, exists_eq_or_imp, incidenceSet, Set.mem_setOf_eq]
  constructor
  Â· rintro âŸ¨he, (hve | âŸ¨w, hwv, hweâŸ©)âŸ©
    Â· exact âŸ¨Sym2.Mem.other hve,
        âŸ¨by rwa [â†mem_edgeSet, Sym2.other_spec, â†mem_edgeFinset],
         âŸ¨mem_edgeFinset.mp he, Sym2.other_mem _âŸ©âŸ©âŸ©
    Â· exact âŸ¨w, âŸ¨hwv, âŸ¨mem_edgeFinset.mp he, hweâŸ©âŸ©âŸ©
  Â· exact fun âŸ¨w, âŸ¨hAdj, âŸ¨he, hweâŸ©âŸ©âŸ© â†¦ âŸ¨mem_edgeFinset.mpr he, Or.inr âŸ¨w, âŸ¨hAdj, hweâŸ©âŸ©âŸ©

lemma incident_closedNeighbor_iff (v : V) :
    G.EdgeIncidenceFinset (G.closedNeighborSet v) =
    Finset.biUnion (G.neighborFinset v) (fun u â†¦ (G.incidenceFinset u)):= by
  ext _
  simp [incident_closedNeighbor_mem_iff]

omit [Fintype V] in
lemma triangle_free_neighbors_pairwise_disjoint_incidenceSet (v : V) (hT : G.CliqueFree 3) :
    (G.neighborSet v).PairwiseDisjoint (fun u â†¦ G.incidenceSet u) := by
  rw [Set.pairwiseDisjoint_iff]
  intro u hu w hw âŸ¨e, âŸ¨heu, hewâŸ©âŸ©
  by_contra huw
  exact isIndepSet_neighborSet_of_triangleFree _ hT _ hu hw huw
    (adj_of_mem_incidenceSet _ huw heu hew)

lemma triangle_free_neighbors_pairwise_disjoint_incidenceFinset (v : V) (hT : G.CliqueFree 3) :
    (G.neighborSet v).PairwiseDisjoint (fun u â†¦ G.incidenceFinset u) := by
  have this := triangle_free_neighbors_pairwise_disjoint_incidenceSet (v := v) hT
  rw [Set.pairwiseDisjoint_iff, pairwiseDisjoint_iff] at *
  exact fun u hu w hw âŸ¨e, heâŸ© â†¦
    (this hu hw âŸ¨e, by rwa [mem_inter, mem_incidenceFinset, mem_incidenceFinset] at heâŸ©)

lemma incident_closedNeighbor_card (hT : G.CliqueFree 3) (v : V) :
    #(G.EdgeIncidenceFinset (G.closedNeighborSet v)) =
    âˆ‘ u âˆˆ G.neighborFinset v, G.degree u := by
  rw [incident_closedNeighbor_iff, card_biUnion ?_]
  Â· simp only [card_incidenceFinset_eq_degree]
  Â· convert (triangle_free_neighbors_pairwise_disjoint_incidenceFinset v hT)
    ext _; simp

lemma incidence_closedNeighbor_expectation (hT : G.CliqueFree 3)
    : ğ”¼ v, (#(G.EdgeIncidenceFinset (G.closedNeighborSet v)) : â„š) = ğ”¼ v, (G.degree v : â„š)^2 := by
  conv_lhs => rhs; intro; rw [incident_closedNeighbor_card hT, Nat.cast_sum, â†Fintype.sum_ite_mem]
  rw [expect_sum_comm]
  conv_lhs => rhs; intro; rhs; intro; rw [â†mul_boole]
  conv_lhs => rhs; intro; rw [â†mul_expect, expect, sum_boole, mul_smul_comm]
  rw [expect, smul_sum]; congr 1; ext; rw [pow_two]; congr 4
  ext; simp [mem_neighborFinset, adj_comm]

lemma incidence_closedNeighbor_expectation_real (hT : G.CliqueFree 3)
    : ğ”¼ v, (#(G.EdgeIncidenceFinset (G.closedNeighborSet v)) : â„) = ğ”¼ v, (G.degree v : â„š)^2 := by
  rw [â†incidence_closedNeighbor_expectation hT]
  convert (algebraMap.coe_expect (M := â„š) (N := â„) _ _).symm

lemma averageDegree_square_bound : d(G) ^ 2 â‰¤ ğ”¼ v, (G.degree v : â„š)^2 := by
  convert expect_mul_sq_le_sq_mul_sq (f := fun v â†¦ (G.degree v : â„š)) (g := fun _ â†¦ 1) univ
  Â· simp only [averageDegree, mul_one]
  Â· by_cases h_nonempty : Nonempty V
    Â· rw [expect_const (univ_nonempty_iff.2 h_nonempty), one_pow, mul_one]
    Â· simp_all




/-
  ## Recursions for Î± and i
-/


lemma bot_Is_independent : âŠ¥ âˆˆ â„(G) := by
  rw [I_rw]
  exact fun _ hx _ _ _ _ â†¦ hx

lemma indepSetFinsetAll_nonempty : â„(G).Nonempty :=
  âŸ¨âŠ¥, bot_Is_independentâŸ©

lemma indepSetFinsetAll_subset {Sâ‚ Sâ‚‚ : Set V} (hind : Sâ‚ âˆˆ â„(G)) (hsub : Sâ‚‚ âŠ† Sâ‚)
    : Sâ‚‚ âˆˆ â„(G) := by
  rw [I_rw] at *
  exact fun  x hx y hy hxy â†¦ hind (hsub hx) (hsub hy) hxy

lemma IsIndependentInduce (S : Set V) (I : Set S) :
    I âˆˆ â„(G.induce S) â†” (Subtype.val '' I) âˆˆ â„(G) := by
  simp_rw [I_rw, IsIndepSet, comap_adj, Function.Embedding.subtype_apply]
  constructor
  Â· intro h _ hx _ hy hxy
    rw [Subtype.coe_image] at hx hy
    exact h hx.2 hy.2 (Subtype.coe_ne_coe.mp hxy)
  Â· intro h _ hx _ hy hxy
    exact h (Set.mem_image_of_mem _ hx) (Set.mem_image_of_mem _ hy) (Subtype.coe_ne_coe.mpr hxy)

lemma IsIndependentInduce' {S I : Set V} (hIS : I âŠ† S) :
    {x | x.1 âˆˆ I} âˆˆ â„(G.induce S) â†” I âˆˆ â„(G):= by
  rw [IsIndependentInduce, Subtype.coe_image_of_subset hIS]

def embeddingOfSubsetSubtype (S : Set V) [DecidablePred (Â· âˆˆ S)] : Set S â†ª Set V where
  toFun := fun I â†¦ Subtype.val '' I
  inj' := by intro _ _; simp only [Set.image_val_inj, imp_self]

omit [Fintype V] in
lemma embeddingOfSubsetSubtype_rw (S : Set V) (I : Set S) :
    embeddingOfSubsetSubtype S I = Subtype.val '' I := rfl

omit [Fintype V] in
lemma set_subtype_tofinset_card_iff {S : Set V} {I : Set S}
      : I.ncard = (Subtype.val '' I).ncard:=
    (Set.ncard_image_of_injective I Subtype.val_injective).symm

lemma IsIndependentInduce_map (S : Set V) : map (embeddingOfSubsetSubtype S) â„(G.induce S)
    = (filter (Â· âŠ† S) â„(G)) := by
  ext I
  simp_rw [mem_map, mem_filter, embeddingOfSubsetSubtype_rw, IsIndependentInduce]
  exact âŸ¨ fun âŸ¨_, âŸ¨hI, hII'âŸ©âŸ© â†¦ (by rw [â†hII']; exact âŸ¨hI, Subtype.coe_image_subset _ _âŸ©),
          fun âŸ¨_, _âŸ© â†¦ âŸ¨{x | x.1 âˆˆ I}, by simp_allâŸ©âŸ©

lemma IsIndependentInduce_implies {S : Set V} {I : Set S} (hI : I âˆˆ â„(induce S G)) :
    Subtype.val '' I âˆˆ â„(G) âˆ§ Subtype.val '' I âŠ† S := by simp_all [IsIndependentInduce]

lemma induced_inedependent_set_count (S : Set V) [DecidablePred (Â· âˆˆ S)] :
    #â„(G.induce S) = #(filter (Â· âŠ† S) â„(G)) := by
  rw [â†IsIndependentInduce_map, Finset.card_map]
  congr
  exact Subsingleton.elim _ _

lemma mem_of_indepSetFinsetAll_card_le_indepNum {I : Set V} (hI : I âˆˆ â„(G))
    : I.ncard â‰¤ Î±(G) := by
  rw [Set.ncard_eq_toFinset_card' I]
  exact IsIndepSet.card_le_indepNum (by rw [I_rw] at hI; rwa [Set.coe_toFinset])

variable (G) in
lemma exists_mem_indepSetFinsetAll_card_indepNum
    : âˆƒ I âˆˆ â„(G), I.ncard = Î±(G) := by
  have âŸ¨_, âŸ¨_, card_eqâŸ©âŸ© := G.exists_isNIndepSet_indepNum
  exact âŸ¨_, âŸ¨by rwa [I_rw], by rwa [Set.ncard_coe_finset]âŸ©âŸ©

/-The independent set version of `isClique_insert`.-/
lemma insert_mem_indepSetFinsetAll_iff {S : Set V}
    : (insert v S) âˆˆ â„(G) â†” S âˆˆ â„(G) âˆ§ âˆ€ u âˆˆ S, Â¬G.Adj v u := by
  simp_rw [I_rw, â†isClique_compl, isClique_insert, isClique_compl, compl_adj]
  exact âŸ¨ fun âŸ¨hI, hâŸ© â†¦ âŸ¨hI, fun _ hu hAdj â†¦ (h _ hu (Adj.ne' hAdj).symm).2 hAdjâŸ©,
          fun âŸ¨hI, hâŸ© â†¦ âŸ¨hI, fun _ hu hvu â†¦ âŸ¨hvu, h _ huâŸ©âŸ©âŸ©

lemma indepSet_augment {v : V} {I : Set V} (hI : I âˆˆ â„(G)) :
    I âŠ† (G.closedNeighborSet v)á¶œ â†” v âˆ‰ I âˆ§ (insert v I) âˆˆ â„(G) := by
  rw [insert_mem_indepSetFinsetAll_iff]
  exact âŸ¨fun hI_sub â†¦ âŸ¨fun hIv â†¦ hI_sub hIv (Set.mem_insert _ _),
      âŸ¨hI, fun u hu hAdj â†¦ (hI_sub hu (Set.mem_insert_of_mem _ hAdj))âŸ©âŸ©,
        fun âŸ¨hvI, âŸ¨_, hAdjâŸ©âŸ© u huI huIn â†¦
       hAdj u huI (Adj_of_mem_closedNeighborSet_of_ne_v (by grind only) huIn)âŸ©

omit [Fintype V] in
lemma indepSet_insert_bound [Finite V] (v : V) :
    Î±(G) â‰¥ Î±(G.induce (G.closedNeighborSet v)á¶œ) + 1 := by
  cases nonempty_fintype V
  have âŸ¨I, hI, hI_cardâŸ© :=
    (induce (G := G) (G.closedNeighborSet v)á¶œ).exists_mem_indepSetFinsetAll_card_indepNum
  rw [â†hI_card, ge_iff_le]
  have this := IsIndependentInduce_implies (by convert hI)
  rw [indepSet_augment this.1] at this
  convert mem_of_indepSetFinsetAll_card_le_indepNum this.2.2
  rw [(Set.ncard_insert_of_notMem this.2.1), â†set_subtype_tofinset_card_iff]


lemma indepSet_card_recursion (v : V)
    : #â„(G) = #â„(G.induce {v}á¶œ) + #â„(G.induce (G.closedNeighborSet v)á¶œ) := by
  rw [add_comm]
  convert (Finset.card_filter_add_card_filter_not (p := (v âˆˆ Â·))).symm
  Â· convert induced_inedependent_set_count (G := G) (S := (G.closedNeighborSet v)á¶œ) using 1
    symm
    refine Finset.card_bij (i := fun S _ â†¦ insert v S) ?_ ?_ ?_
    Â· intro S hS
      simp only [mem_filter, Set.mem_insert_iff, true_or, and_true]
      rw [mem_filter] at hS
      exact ((indepSet_augment hS.1).1 hS.2).2
    Â· intro Sâ‚ hSâ‚ Sâ‚‚ hSâ‚‚ heq
      rw [mem_filter] at *
      refine Set.Subset.antisymm ?_ ?_ <;> rw [â†Set.insert_subset_insert_iff (a := v) ?_]
      Â· exact Eq.subset heq
      Â· exact fun hv â†¦ hSâ‚.2 hv self_mem_closedNeighborSet
      Â· exact Eq.subset heq.symm
      Â· exact fun hv â†¦ hSâ‚‚.2 hv self_mem_closedNeighborSet
    Â· intro S hS
      use S \ {v}
      simp only [Set.insert_diff_singleton, Set.insert_eq_self, mem_filter,
        Set.diff_singleton_subset_iff, exists_prop]
      simp_rw [mem_filter] at hS
      refine âŸ¨âŸ¨indepSetFinsetAll_subset hS.1 Set.diff_subset,?_âŸ©, hS.2âŸ©
      intro x hx
      refine Set.mem_insert_iff.mpr ?_
      rw [or_iff_not_imp_left]
      intro hxv hxNbr
      rw [I_rw] at hS
      apply hS.1 hx hS.2 hxv (Adj_of_mem_closedNeighborSet_of_ne_v (fun a â†¦ hxv a.symm) hxNbr).symm
  Â· convert induced_inedependent_set_count (S := {v}á¶œ)
    simp
  Â· exact inferInstance
  Â· exact inferInstance


/-
  ## exists lemma
-/

/- `le` version of `exists_lt_of_lt_expect`-/
lemma exists_ge_of_le_expect {a : â„} {g : V â†’ â„} (h_nonempty : Nonempty V) (h : a â‰¤ ğ”¼ i, g i)
  : âˆƒ x, a â‰¤ g x := by
  have âŸ¨x, _, h_allâŸ© := exists_max_image (s := univ) (f := g) (univ_nonempty_iff.mpr h_nonempty)
  exact âŸ¨x, le_trans h (expect_le (univ_nonempty_iff.mpr h_nonempty) h_all)âŸ©

lemma Jensen_expect {Î± : Type} {t : Finset Î±} (ht : t.Nonempty) {s : Set â„} {f : â„ â†’ â„} (p : Î± â†’ â„)
    (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) (hf : ConvexOn â„ s f) : f (ğ”¼ i âˆˆ t, p i) â‰¤ ğ”¼ i âˆˆ t, f (p i) := by
  have hc : (0 : â„) < #t := Nat.cast_pos.2 (card_pos.2 ht)
  have hrw : âˆ€ g : Î± â†’ â„, ğ”¼ i âˆˆ t, g i = âˆ‘ i âˆˆ t, (#t : â„)â»Â¹ â€¢ g i := by
    intro g; simp_rw [expect_eq_sum_div_card, sum_div, smul_eq_mul, mul_comm, div_eq_mul_inv]
  rw [hrw, hrw]
  exact hf.map_sum_le (fun _ _ â†¦ inv_nonneg.2 hc.le)
    (by simp [sum_const, nsmul_eq_mul, mul_inv_cancelâ‚€ hc.ne']) hmem

lemma exp_expect_le_expect_exp (g : V â†’ â„) (hV : Nonempty V)
    : Real.exp (ğ”¼ v, g v) â‰¤ ğ”¼ v, Real.exp (g v) :=
  Jensen_expect (univ_nonempty_iff.2 hV) _ (by simp) convexOn_exp


/-
  ## Shearer Step
-/

variable (G) in
noncomputable def extra (f f' : â„ â†’ â„) (a : â„) (b : â„) :=
  if Nonempty V then a * (d(G) * f' d(G) - f d(G)) + b * (- 2 * f' d(G))
                else 0

lemma extra_nonempty {f f' : â„ â†’ â„} {a : â„} {b : â„} (hV : Nonempty V) :
    extra G f f' a b = a * (d(G) * f' d(G) - f d(G)) + b * (- 2 * f' d(G)) :=
  if_pos hV

lemma extra_IsEmpty {f f' : â„ â†’ â„} {a : â„} {b : â„} (hV : IsEmpty V) :
    extra G f f' a b = 0 := if_neg (not_nonempty_iff.2 hV)

lemma extra_rewrite_aux {f f' : â„ â†’ â„} (hV : Nonempty V) : G.extra f f' (d(G) + 1) (d(G) ^ 2)
    = ((d(G)) - d(G)^2) * f' (d(G)) - ((d(G)) + 1) * f d(G) := by
  rw [extra_nonempty hV]
  ring

lemma extra_rewrite_aux' {f f' : â„ â†’ â„} (hV : Nonempty V) : G.extra f f' 1 d(G)
    = - (d(G)) * f' (d(G)) - f (d(G)) := by
  rw [extra_nonempty hV]
  ring


variable (G) in
lemma extra_expect {f f' : â„ â†’ â„} {gâ‚ gâ‚‚ : V â†’ â„} :
    ğ”¼ v, extra G f f' (gâ‚ v) (gâ‚‚ v) = extra G f f' (ğ”¼ v, gâ‚ v) (ğ”¼ v, gâ‚‚ v) := by
  unfold extra
  split_ifs
  Â· rw [expect_add_distrib, â†expect_mul, â†expect_mul]
  Â· exact expect_const_zero _

lemma extra_finset_zero {f f' : â„ â†’ â„} (S : Set V) (h : d(G) = 0)
    : extra G f f' (#S.toFinset) 0 = -(#S.toFinset) * f 0 := by
  unfold extra
  split_ifs <;> simp_all

lemma extra_ineq {a b c : â„} {f f' : â„ â†’ â„} (hf' : âˆ€ x, 0 < x â†’ f' x â‰¤ 0) (hab : b â‰¤ c)
    (hdG : 0 < d(G)) :
    extra G f f' a b â‰¤ extra G f f' a c := by
  unfold extra
  split_ifs
  Â· gcongr
    linarith [hf' d(G) (Rat.cast_pos.mpr hdG)]
  Â· exact le_refl _

noncomputable def convexIneq (f f' : â„ â†’ â„) :=
    âˆ€ x y, 0 â‰¤ x â†’ 0 < y â†’ f x â‰¥ f y + f' y * (x - y)

lemma shearer_convex_step {Vâ‚ Vâ‚‚ : Type} [Fintype Vâ‚] [Fintype Vâ‚‚]
    {Hâ‚ : SimpleGraph Vâ‚} {Hâ‚‚ : SimpleGraph Vâ‚‚} (f f' : â„ â†’ â„) (hf : convexIneq f f')
    (hdHâ‚ : 0 < d(Hâ‚)) : #V(Hâ‚‚) * f d(Hâ‚‚) â‰¥
    #V(Hâ‚) * f d(Hâ‚) + extra Hâ‚ f f' (#V(Hâ‚) - #V(Hâ‚‚)) (#E(Hâ‚) - #E(Hâ‚‚))
  := by
  have h_ineq := hf d(Hâ‚‚) d(Hâ‚) (Rat.cast_nonneg.2 averageDegree_nonneg) (Rat.cast_pos.2 hdHâ‚)
  have cast_rw (a : â„•) : (a : â„) = ((a : â„š) : â„) := rfl
  refine le_trans ?_ (mul_le_mul_of_nonneg_left h_ineq (Nat.cast_nonneg _))
  rw [extra_nonempty (vertexType_Nonempty_from_averageDegree_pos hdHâ‚),
      â†mul_assoc, mul_comm (b := -2), neg_mul_comm, neg_sub,
      mul_sub (a := 2), â†Nat.cast_ofNat, â†Nat.cast_mul, â†Nat.cast_mul, cast_rw (2 * #E(Hâ‚‚)),
      Nat.cast_mul,Nat.cast_ofNat, â†averageDegree_eq_twice_card_edges_div_card,
      cast_rw (2 * #E(Hâ‚)), Nat.cast_mul,Nat.cast_ofNat,
      â†averageDegree_eq_twice_card_edges_div_card, Rat.cast_mul, Rat.cast_natCast,
      Rat.cast_mul, Rat.cast_natCast, â†sub_nonneg]
  convert le_refl _
  ring

lemma convex_step_induce (S : Set V) {f f' : â„ â†’ â„} (hf : convexIneq f f')
  : #V(G.induce Sá¶œ) * f d(G.induce Sá¶œ) â‰¥ #V(G) * f d(G) +
    extra G f f' #S.toFinset #(G.EdgeIncidenceFinset S)  := by
    by_cases hd : d(G) = 0
    Â· have this : d(induce Sá¶œ G) = 0 := by
        convert induceAveragedegree_zero_from_averageDegree_zero hd _
      rw [hd, this, Rat.cast_zero,
      EdgeIncidenceFinset_card_zero_from_averageDegree_zero hd, Nat.cast_zero,
      extra_finset_zero _ hd, neg_mul, ge_iff_le, add_neg_le_iff_le_add, â†add_mul,
      â†Nat.cast_add, add_comm, induce_vertex_card, (vertex_card_induce_sum S)]
    Â· convert shearer_convex_step (Hâ‚ := G) (Hâ‚‚ := G.induce Sá¶œ) f f' hf
        (averageDegree_pos_from_nonzero hd) <;>
      rw [eq_sub_iff_add_eq, â†Nat.cast_add, Nat.cast_inj]
      Â· rw [induce_vertex_card]
        exact (vertex_card_induce_sum S).symm
      Â· exact (edge_card_induce_sum S).symm

lemma convex_step_expect {F : V â†’ Set V} {f f' : â„ â†’ â„} (hf : convexIneq f f')
  : ğ”¼ v, #V(G.induce (F v)á¶œ) * f d(G.induce (F v)á¶œ) â‰¥ #V(G) * f d(G) +
    extra G f f' (ğ”¼ v, #(F v).toFinset) (ğ”¼ v, #(G.EdgeIncidenceFinset (F v))) := by
  rw [ge_iff_le]
  convert expect_le_expect
    (f := fun v â†¦ #V(G) * f d(G) + extra G f f' #(F v).toFinset #(G.EdgeIncidenceFinset (F v)))
    (fun v hv â†¦ convex_step_induce _ hf) using 1
  rw [expect_add_distrib, extra_expect]
  congr 1
  by_cases hV : #V(G) = 0
  Â· simp [hV]
  Â· exact (expect_const (card_ne_zero.mp hV) _).symm

lemma convex_step_expect_closedNeighbor {f f' : â„ â†’ â„} (hf : convexIneq f f')
    (hT : G.CliqueFree 3) (hf' : âˆ€ (x : â„), 0 < x â†’ f' x â‰¤ 0)
    : ğ”¼ v, #V(G.induce (G.closedNeighborSet v)á¶œ) * f d(G.induce (G.closedNeighborSet v)á¶œ)
    â‰¥ #V(G) * f d(G) + extra G f f' (d(G) + 1) (d(G) ^ 2) := by
  by_cases hV : Nonempty V
  Â· refine le_trans ?_ (convex_step_expect (F := fun v â†¦ G.closedNeighborSet v) hf)
    gcongr
    rw [expect_closedNeighbor_card_real hV, incidence_closedNeighbor_expectation_real hT,
      â†Rat.cast_pow]
    by_cases hdG : d(G) = 0
    Â· simp [hdG, degree_zero_of_avg_zero]
    Â· exact extra_ineq hf'  (Rat.cast_le.2 averageDegree_square_bound)
                            (averageDegree_pos_from_nonzero hdG)
  Â· simp_all [extra_IsEmpty, card_vertices_eq_zero_from_empty]

theorem Shearer_bound (hT : G.CliqueFree 3) {f f' : â„ â†’ â„} (hf : convexIneq f f')
    (hf' : âˆ€ (x : â„), 0 < x â†’ f' x â‰¤ 0)
    (h_extra : âˆ€ {V' : Type} [Fintype V'] {G' : SimpleGraph V'},
      0 â‰¤ G'.extra f f' (d(G') + 1) (d(G') ^ 2) + 1)
    : Î±(G) â‰¥ #V(G) * f d(G) := by
  suffices h : âˆ€ n, âˆ€ {V' : Type} [Fintype V'] {G' : SimpleGraph V'} (hn : n = #V(G'))
    (hT : G'.CliqueFree 3), Î±(G') â‰¥ n * (f d(G')) from (h _ rfl hT)
  intro n
  induction n using Nat.strong_induction_on with | _ n hn =>
  intro V _ G hcard hT
  by_cases hV : Nonempty V
  Â· rw [â†expect_const (univ_nonempty_iff.mpr hV) (Î±(G) : â„)]
    calc
      ğ”¼ v, (Î±(G) : â„)   â‰¥ ğ”¼ v, ((Î±(G.induce (G.closedNeighborSet v)á¶œ) + 1 : â„•) : â„) :=
        expect_le_expect (fun _ _ â†¦ (Nat.cast_le.2 (indepSet_insert_bound _)))
      _                 = ğ”¼ v, (Î±(G.induce (G.closedNeighborSet v)á¶œ) + 1 : â„) := by simp
      _                 = (ğ”¼ v, (Î±(G.induce (G.closedNeighborSet v)á¶œ) : â„)) + 1 := by
        rw [expect_add_distrib, expect_const (univ_nonempty_iff.mpr hV) _]
      _                 â‰¥ (ğ”¼ v, #V(G.induce (G.closedNeighborSet v)á¶œ) *
                          f d(G.induce (G.closedNeighborSet v)á¶œ)) + 1 := by
        gcongr;
        apply hn
        Â· rw [hcard]
          exact induce_vertex_card_compl_lt nonempty_closedNeighborSet
        Â· rfl
        Â· exact CliqueFree.comap (Embedding.induce _) hT
      _                 â‰¥ #V(G) * f d(G) + extra G f f' (d(G) + 1) (d(G) ^ 2) + 1 := by
        gcongr;
        exact convex_step_expect_closedNeighbor hf hT hf'
      _                 â‰¥ #V(G) * f d(G) + 0  := by
        rw [add_assoc]
        gcongr
        exact h_extra
      _                 = #V(G) * f d(G)      := add_zero _
    rw [hcard]
  Â· rw [card_vertices_eq_zero_from_empty (not_nonempty_iff.mp hV)] at hcard
    simp [hcard]


/-
  ## The result for independent set count
-/

lemma convex_step_expect_vertex_complement {f f' : â„ â†’ â„} (hf : convexIneq f f')
    : ğ”¼ v, #V(G.induce {v}á¶œ) * f d(G.induce {v}á¶œ)
    â‰¥ #V(G) * f d(G) + extra G f f' 1 d(G) := by
  by_cases hV : Nonempty V
  Â· convert (convex_step_expect (F := fun v â†¦ {v}) hf)
    Â· simp
    Â· simp [edge_incidence_singelton_card, averageDegree_real_cast]
  Â· simp_all [card_vertices_eq_zero_from_empty, extra_IsEmpty]

theorem independent_set_count_bound (hT : G.CliqueFree 3) {f f' : â„ â†’ â„}
    (hf : convexIneq f f')
    (hf' : âˆ€ (x : â„), 0 < x â†’ f' x â‰¤ 0)
    (h_extra : âˆ€ {V' : Type} [Fintype V'] {G' : SimpleGraph V'},
      1 â‰¤
        Real.exp (G'.extra f f' 1 d(G')) +
        Real.exp (G'.extra f f' (d(G') + 1) (d(G') ^ 2)))
    : #â„(G) â‰¥ Real.exp (#V(G) * f d(G)) := by
  suffices h : âˆ€ n, âˆ€ {V' : Type} [Fintype V'] {G' : SimpleGraph V'} (hn : n = #V(G'))
    (hT : G'.CliqueFree 3), #â„(G') â‰¥ Real.exp (#V(G') * f d(G')) from (h _ rfl hT)
  intro n
  induction n using Nat.strong_induction_on with | _ n hn =>
  intro V _ G hcard hT
  by_cases hV : Nonempty V
  Â· rw [â†expect_const (univ_nonempty_iff.mpr hV) (#â„(G) : â„)]
    calc
      ğ”¼ v, (#â„(G) : â„)
            = ğ”¼ v, (#â„(G.induce {v}á¶œ) + #â„(G.induce (G.closedNeighborSet v)á¶œ) : â„ ) := by
        apply expect_congr rfl
        exact_mod_cast fun _ _ â†¦ indepSet_card_recursion _
      _   =   ğ”¼ v, (#â„(G.induce {v}á¶œ) : â„)
            + ğ”¼ v, (#â„(G.induce (G.closedNeighborSet v)á¶œ) : â„ )  := expect_add_distrib _ _ _
      _   â‰¥   ğ”¼ v, Real.exp (#V(G.induce {v}á¶œ) * f d(G.induce {v}á¶œ))
            + ğ”¼ v, Real.exp (#V(G.induce (G.closedNeighborSet v)á¶œ)
                              * f d(G.induce (G.closedNeighborSet v)á¶œ)) := by
        gcongr <;> rename_i v hv
        Â· apply hn (#V(G.induce {v}á¶œ))
          Â· rw [hcard]
            refine induce_vertex_card_compl_lt âŸ¨v, rflâŸ©
          Â· rfl
          Â· exact CliqueFree.comap (Embedding.induce _) hT
        Â· apply hn (#V(G.induce (G.closedNeighborSet v)á¶œ))
          Â· rw [hcard]
            refine induce_vertex_card_compl_lt nonempty_closedNeighborSet
          Â· rfl
          Â· exact CliqueFree.comap (Embedding.induce _) hT
      _   â‰¥   Real.exp (ğ”¼ v, #V(G.induce {v}á¶œ) * f d(G.induce {v}á¶œ))
            + Real.exp (ğ”¼ v, #V(G.induce (G.closedNeighborSet v)á¶œ)
                              * f d(G.induce (G.closedNeighborSet v)á¶œ)) := by
        gcongr <;> exact exp_expect_le_expect_exp _ hV
      _   â‰¥   Real.exp (#V(G) * f d(G) + extra G f f' 1 d(G))
            + Real.exp (#V(G) * f d(G) + extra G f f' (d(G) + 1) (d(G) ^ 2)) := by
        gcongr
        Â· exact convex_step_expect_vertex_complement hf
        Â· exact convex_step_expect_closedNeighbor hf hT hf'
      _   = Real.exp (#V(G) * f d(G)) *
            (Real.exp (extra G f f' 1 d(G)) + Real.exp (extra G f f' (d(G) + 1) (d(G) ^ 2))) := by
        rw [Real.exp_add, Real.exp_add, mul_add]
      _                 â‰¥ Real.exp (#V(G) * f d(G)) := by
        nth_rewrite 2 [â†mul_one (a := Real.exp (â†‘(#V(G)) * f â†‘d(G)))]
        gcongr
        exact h_extra
  Â· simp [card_vertices_eq_zero_from_empty (not_nonempty_iff.mp hV), indepSetFinsetAll_nonempty]


end SimpleGraph
