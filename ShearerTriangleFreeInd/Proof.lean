/-
Copyright (c) 2024 Pjotr Buys. All rights reserved.
Authors: Pjotr Buys
-/

import Mathlib
import ShearerTriangleFreeInd.Analysis

/-!
# The Independence Number of Triangle-Free Graphs

This file proves Shearer's theorem on the independence number of triangle-free graphs.

We prove that if `G` is a triangle-free graph on `n` vertices with average degree `d`,
then its independence number `Î±` satisfies `Î± â‰¥ n Â· F(d)` where `F` is the Shearer function
defined by `F(x) = (x ln x - x + 1) / (x - 1)Â²` for `x â‰  1` and `F(1) = 1/2`.

In this file the theorem is called `triangle_free_independence_bound`.

## Main definitions

* `closedNeighborSet G v` : The closed neighborhood of `V`.
* `puncture G v`: The induced subgraph obtained by removing the closed neighborhood of `v`,
    this is a graph with vertex type the subtype `(G.closedNeighborSet v)á¶œ` of `V`.
* `averageDegree G`: The average degree of vertices in graph `G`, defined as type `â„š`.

## Implementation notes

The proof follows the proof of Theorem 1 given in [shearer1983]. The most challenging
part was proving the convexity of `F`. This is done in the companion file `Analysis`.

## References

* [J. B. Shearer, *A note on the independence number of triangle-free graphs*,
  Discrete Mathematics 46 (1983) 83-87][shearer1983]
-/


open Finset SimpleGraph
open Classical
open BigOperators


namespace SimpleGraph

variable {V : Type}

/-!
### Definition of puncture
-/

/-- The closed neighborhood of a vertex consists of the vertex and its neighbors. -/
def closedNeighborSet (G : SimpleGraph V) (v : V) := insert v (G.neighborSet v)

/-- The exterior of v is the set of vertices outside v's closed neighborhood. -/
def exterior (G : SimpleGraph V) (v : V) := (G.closedNeighborSet v)á¶œ

/-- The puncture of G at v is the induced subgraph on vertices outside v's closed neighborhood. -/
def puncture (G : SimpleGraph V) (v : V) := G.induce (G.exterior v)

/-!
  ## For coercions.
-/

def Ï† {G : SimpleGraph V} {v : V} : G.exterior v â†ª V  := Function.Embedding.subtype _
def Ï†G {G : SimpleGraph V} {v : V} : G.puncture v â†ªg G := Embedding.induce _

lemma finset_coe_subtype {G : SimpleGraph V} {v : V} (S : Finset (G.exterior v)) :
    (S.map Ï† : Set V)  = Subtype.val '' (S : Set (G.exterior v)) := coe_map _ _

/-!
## Basic lemmas about G.puncture v without assuming finiteness of V
-/

variable {G : SimpleGraph V} {v : V}

lemma mem_closedNeighborSet_self : v âˆˆ G.closedNeighborSet v := Set.mem_insert _ _

lemma not_mem_exterior_self : v âˆ‰ G.exterior v := fun hvw â†¦ hvw mem_closedNeighborSet_self

lemma adj_mem_closedNeighborSet {w : V} (hw : G.Adj v w) : w âˆˆ G.closedNeighborSet v :=
  Set.mem_insert_of_mem _ hw

lemma not_adj_of_mem_exterior {w : V} (hw : w âˆˆ G.exterior v) : Â¬ G.Adj v w :=
  fun hvw â†¦ hw (adj_mem_closedNeighborSet hvw)

lemma not_mem_image_edge_of_neighbor (e : Sym2 (G.exterior v)) :
    âˆ€ u, u âˆˆ G.neighborSet v â†’ u âˆ‰ (Ï†.sym2Map e) := by
  intro _ hu hue
  have âŸ¨u, _, hueqâŸ© := Sym2.mem_map.1 hue
  exact not_adj_of_mem_exterior (by rw [â†hueq]; exact Subtype.coe_prop u) hu

lemma mem_exterior_of_mem_edge_disjoint_neighbors {e : Sym2 V} (he : e âˆˆ G.edgeSet)
    (hu_not : âˆ€ u, u âˆˆ G.neighborSet v â†’ u âˆ‰ e) : âˆ€ u âˆˆ e, u âˆˆ G.exterior v := by
  intro u hue hu_clnbrv
  rcases Set.mem_insert_iff.1 hu_clnbrv with huv | hu_nbrv
  Â· rw [huv] at hue
    exact hu_not (Sym2.Mem.other hue) ((mem_incidence_iff_neighbor _).1
      âŸ¨by rwa [Sym2.other_spec hue], Sym2.mem_mk_left v _âŸ©) (Sym2.other_mem hue)
  Â· exact (hu_not _ hu_nbrv) hue

lemma exists_preimage_of_edge_disjoint_neighbors {e : Sym2 V}
    (he : e âˆˆ G.edgeSet) (hu_not : âˆ€ u, u âˆˆ G.neighborSet v â†’ u âˆ‰ e)
    : âˆƒ e' âˆˆ (G.puncture v).edgeSet, (Ï†.sym2Map e') = e := by
  have he_eq : Ï†.sym2Map (e.attachWith (mem_exterior_of_mem_edge_disjoint_neighbors he hu_not)) = e
    := Sym2.attachWith_map_subtypeVal _
  rw [â†he_eq] at he
  exact âŸ¨_, âŸ¨(Embedding.map_mem_edgeSet_iff Ï†G).mp he, he_eqâŸ©âŸ©

variable (G) (v) in
lemma puncture_edgeSet_image_eq : Ï†.sym2Map '' (G.puncture v).edgeSet =
    G.edgeSet \ â‹ƒ u âˆˆ (G.neighborSet v), (G.incidenceSet u) := by
  ext e
  rw [Set.mem_diff, Set.mem_iUnion]
  constructor
  Â· intro âŸ¨_, he', he_eqâŸ©
    rw [â†he_eq]
    exact âŸ¨(Embedding.map_mem_edgeSet_iff Ï†G).mpr he',
      fun âŸ¨_, _, âŸ¨huv, htâŸ©, hueâŸ© â†¦
        (by rw [â†ht] at hue; exact not_mem_image_edge_of_neighbor _ _ huv hue.2)âŸ©
  Â· exact fun âŸ¨he, he_notâŸ© â†¦ (Set.mem_image _ _ _).2
      (exists_preimage_of_edge_disjoint_neighbors he
      fun u hu hue â†¦ he_not âŸ¨u, Set.mem_iUnion.2 âŸ¨hu, âŸ¨he, hueâŸ©âŸ©âŸ©)

/-!
## Two general lemmas
-/

lemma neighborSet_pairwiseDisjoint_incidenceSet (hT : G.CliqueFree 3) :
    (G.neighborSet v).PairwiseDisjoint (fun u â†¦ G.incidenceSet u) := by
  rw [Set.pairwiseDisjoint_iff]
  intro u hu w hw âŸ¨e, âŸ¨heu, hewâŸ©âŸ©
  by_contra huw
  exact isIndepSet_neighborSet_of_triangleFree _ hT _ hu hw huw
    (adj_of_mem_incidenceSet _ huw heu hew)

/-The independent set version of `isClique_insert`.-/
lemma isIndepSet_insert {T : Set V}
    : G.IsIndepSet (insert v T) â†” G.IsIndepSet T âˆ§ âˆ€ u âˆˆ T, v â‰  u â†’ Â¬G.Adj v u := by
  simp_all [â†isClique_compl, â†isClique_compl , isClique_insert]


/-!
## Now we assume that V has Fintype. Lemmas about closedNeighborFinset.
-/

variable [Fintype V]

noncomputable instance fintype_puncture : Fintype (G.exterior v) := Fintype.ofFinite _

variable (G) (v) in
noncomputable def closedNeighborFinset := insert v (G.neighborFinset v)

lemma closedNeighborFinset_coe : closedNeighborFinset G v = G.closedNeighborSet v := by
  simp [closedNeighborFinset, closedNeighborSet, neighborFinset_def]

lemma closedNeighborSet_toFinset : (G.closedNeighborSet v).toFinset = closedNeighborFinset G v := by
  rw [â†closedNeighborFinset_coe, toFinset_coe]

lemma puncture_verts_toFinset : (G.exterior v).toFinset = univ \ G.closedNeighborFinset v := by
  simp only [exterior, Set.toFinset_compl, closedNeighborSet_toFinset, compl_eq_univ_sdiff]

lemma closedNeighborFinset_card : #(G.closedNeighborFinset v) = G.degree v + 1 :=
  card_insert_of_notMem (notMem_neighborFinset_self _ _)

lemma V_card_ge_deg_plus_one : Fintype.card V â‰¥ G.degree v + 1 := by
  rw [â†closedNeighborFinset_card]
  exact card_le_univ _

lemma puncture_verts_card : Fintype.card (G.exterior v) = Fintype.card V - (G.degree v + 1) := by
  rw [â†Set.toFinset_card, puncture_verts_toFinset, card_univ_diff, closedNeighborFinset_card]

lemma puncture_verts_card_rat :
  (Fintype.card (G.exterior v) : â„š) = Fintype.card V - (G.degree v + 1) := by
  rw [puncture_verts_card (G := G) (v := v), Nat.cast_sub V_card_ge_deg_plus_one,
  Nat.cast_add, Nat.cast_one]

lemma puncture_verts_card_lt : Fintype.card (G.exterior v) < Fintype.card V := by
  rw [puncture_verts_card, tsub_lt_self_iff, Fintype.card_pos_iff, add_pos_iff]
  exact âŸ¨Nonempty.intro v, Or.inr Nat.one_posâŸ©

lemma V_empty_edgeFinset_card_zero (hV : IsEmpty V) : #G.edgeFinset = 0 := by
  simp only [Set.toFinset_card, Fintype.card_eq_zero]

variable (G) (v) in
theorem alpha_in_rel : (puncture G v).indepNum + 1 â‰¤ G.indepNum:= by
  have âŸ¨S,hSâŸ© := exists_isNIndepSet_indepNum (G := (puncture G v))
  have hS_coe : (S.map Ï† : Set V) âŠ† G.exterior v := map_subtype_subset _
  convert IsIndepSet.card_le_indepNum (t := insert v (S.map Ï†)) ?_
  Â· rw [â†hS.2, card_insert_of_notMem (fun h â†¦ not_mem_exterior_self (hS_coe h)), card_map]
  Â· rw [coe_insert, isIndepSet_insert]
    refine âŸ¨?_, (fun _ hu _  â†¦ (not_adj_of_mem_exterior (hS_coe hu)))âŸ©
    have this := hS.1
    rwa [puncture, induce_eq_coe_induce_top, isIndepSet_induce, â†finset_coe_subtype] at this




/-!
### The average degree of G.
-/

variable (G) in
/-- The average degree of vertices in a finite graph. -/
noncomputable def averageDegree := ğ”¼ v, (G.degree v : â„š)

lemma averageDegree_nonneg : 0 â‰¤ averageDegree G := expect_nonneg (fun _ _ â†¦ Nat.cast_nonneg' _)

lemma averageDegree_eq_twice_card_edges_div_card :
  (averageDegree G) = 2 * #G.edgeFinset / (Fintype.card V : â„š) := by
  convert Fintype.expect_eq_sum_div_card (fun v â†¦ (G.degree v : â„š))
  convert congrArg (Nat.cast (R := â„š)) (sum_degrees_eq_twice_card_edges G).symm using 1 <;> simp

lemma card_mul_averageDegree_eq_twice_card_edges :
    Fintype.card V  * (averageDegree G) = 2 * #G.edgeFinset := by
  rw [averageDegree_eq_twice_card_edges_div_card]
  rcases Nat.eq_zero_or_pos (Fintype.card V) with h | h
  Â· simp [h, filter_eq_empty_iff]
    rw [Fintype.card_eq_zero_iff, isEmpty_iff] at h
    exact fun x _ â†¦ h (x.out).1
  Â· field_simp

@[simp]
lemma neighborFinset_coe : (G.neighborFinset v : Set V) = G.neighborSet v := by ext _; simp
@[simp]
lemma incidenceFinset_coe : (G.incidenceFinset v : Set (Sym2 V)) = G.incidenceSet v :=
  by ext _; simp

lemma triangleFree_neighbors_disjoint_incidenceFinset (hT : G.CliqueFree 3) :
    (G.neighborSet v).PairwiseDisjoint (fun u â†¦ G.incidenceFinset u) := by
  have this := neighborSet_pairwiseDisjoint_incidenceSet (v := v) hT
  rw [Set.pairwiseDisjoint_iff, pairwiseDisjoint_iff] at *
  exact fun u hu w hw âŸ¨e, heâŸ© â†¦
    (this hu hw âŸ¨e, by rwa [mem_inter, mem_incidenceFinset, mem_incidenceFinset] at heâŸ©)

variable (G) (v) in
lemma puncture_edgeFinset_map_eq : map Ï†.sym2Map (G.puncture v).edgeFinset =
    G.edgeFinset \ Finset.biUnion (G.neighborFinset v) (fun u â†¦ (G.incidenceFinset u)) := by
  rw [â†coe_inj]; convert puncture_edgeSet_image_eq G v <;> simp

lemma neighborhood_incidenceFinset_sub :
    Finset.biUnion (G.neighborFinset v) (fun u â†¦ (G.incidenceFinset u)) âŠ† G.edgeFinset := by
  intro _ he
  have âŸ¨_,_,hâŸ© := mem_biUnion.1 he
  exact (incidenceFinset_subset _ _ h)

lemma puncture_edge_card (hT : G.CliqueFree 3) : (#(G.puncture v).edgeFinset : â„š)
    = #G.edgeFinset - âˆ‘ u âˆˆ G.neighborFinset v, (G.degree u : â„š) := by
  convert congrArg (Nat.cast (R := â„š)) (congrArg card (puncture_edgeFinset_map_eq G v))
  Â· exact (card_map _).symm
  Â· rw [card_sdiff neighborhood_incidenceFinset_sub,
      Nat.cast_sub (card_le_card neighborhood_incidenceFinset_sub), â†Nat.cast_sum, card_biUnion ?_]
    Â· simp
    Â· rw [neighborFinset_coe]
      exact triangleFree_neighbors_disjoint_incidenceFinset hT

lemma expect_sum_degree_neighbors :
    ğ”¼ v, âˆ‘ u âˆˆ G.neighborFinset v, (G.degree u : â„š) = ğ”¼ v, (G.degree v : â„š)^2 := by
  conv => lhs; rhs; intro v ; rw [â†(Fintype.sum_ite_mem _ _)]
  simp_rw [expect_sum_comm]
  conv => lhs; rhs; intro u; rhs; intro i; rw [â†mul_boole]
  conv => lhs; rhs; intro u; rw [â†mul_expect, expect, sum_boole, mul_smul_comm]
  rw [expect, smul_sum]
  congr; ext _; rw [pow_two]; congr 4
  ext _; simp only [mem_neighborFinset, adj_comm, mem_filter, mem_univ, true_and]

lemma expect_puncture_edgeFinset_card (hT : G.CliqueFree 3)
    : (ğ”¼ v, #(G.puncture v).edgeFinset : â„š) = #G.edgeFinset - (ğ”¼ v, (G.degree v : â„š)^2):= by
  simp_rw [puncture_edge_card hT, expect_sub_distrib, expect_sum_degree_neighbors]
  congr
  by_cases hV : Nonempty V
  Â· exact expect_const (univ_nonempty_iff.mpr hV) _
  Â· rw [not_nonempty_iff] at hV
    simp

lemma expect_puncture_edgeFinset_card_real (hT : G.CliqueFree 3)
    : (ğ”¼ v, #(G.puncture v).edgeFinset : â„) = #G.edgeFinset - (ğ”¼ v, (G.degree v : â„)^2):= by
  convert congrArg (Rat.cast (K := â„)) (expect_puncture_edgeFinset_card hT)
  Â· exact (algebraMap.coe_expect univ (fun v â†¦ (#(G.puncture v).edgeFinset : â„š))).symm
  Â· have this := (algebraMap.coe_expect univ (M := â„š) (N := â„) (fun v â†¦ (G.degree v : â„š)^2)).symm
    simp_all

lemma edge_card_zero_iff_average_degree_zero : #G.edgeFinset = 0 â†” G.averageDegree = 0 := by
  rw [â†Rat.natCast_inj, â†(mul_right_inj_of_invertible 2),
    â†card_mul_averageDegree_eq_twice_card_edges, (by rfl: (0: â„•) = (0 : â„š)),
    mul_zero, mul_eq_zero, or_iff_right_iff_imp]
  intro hV
  apply expect_eq_zero
  intro i hi
  rw [Rat.natCast_eq_zero, Fintype.card_eq_zero_iff] at hV
  exact False.elim (IsEmpty.false i)

lemma degree_eq_zero_of_averageDegree_eq_zero (h : G.averageDegree = 0) :
    âˆ€ v, G.degree v = 0 := fun v â†¦ Rat.natCast_eq_zero.mp
  ((expect_eq_zero_iff_of_nonneg âŸ¨v, mem_univ _âŸ© (fun _ _ â†¦ Nat.cast_nonneg' _)).1 h v (mem_univ _))

lemma averageDegree_puncture_eq_zero_of_averageDegree_eq_zero (h : G.averageDegree = 0) :
    (G.puncture v).averageDegree = 0 := by
  rw [â†edge_card_zero_iff_average_degree_zero] at *
  have map_eq := puncture_edgeFinset_map_eq G v
  rw [card_eq_zero] at h
  rwa [h, empty_sdiff, map_eq_empty, â†card_eq_zero] at map_eq

lemma averageDegree_square_bound : G.averageDegree ^ 2 â‰¤ ğ”¼ v, (G.degree v : â„š)^2 := by
  convert expect_mul_sq_le_sq_mul_sq (f := fun v â†¦ (G.degree v : â„š)) (g := fun _ â†¦ 1) univ
  Â· simp only [averageDegree, mul_one]
  Â· by_cases h_nonempty : Nonempty V
    Â· rw [expect_const (univ_nonempty_iff.2 h_nonempty), one_pow, mul_one]
    Â· simp_all


/-!
### Proof of the main statement
-/

lemma exists_ge_of_le_expect {a : â„} {g : V â†’ â„} (h_nonempty : Nonempty V) (h : a â‰¤ ğ”¼ i, g i)
  : âˆƒ x, a â‰¤ g x := by
  have âŸ¨x, _, h_allâŸ© := exists_max_image (s := univ) (f := g) (univ_nonempty_iff.mpr h_nonempty)
  exact âŸ¨x, le_trans h (expect_le (univ_nonempty_iff.mpr h_nonempty) h_all)âŸ©


lemma exists_good_vertex (h_nonempty : Nonempty V) (hT : CliqueFree G 3) :
    âˆƒ v, (Fintype.card V) * (F G.averageDegree)
    â‰¤ 1 + (Fintype.card V - (G.degree v + 1)) * (F (G.puncture v).averageDegree) := by
  by_cases hdâ‚€ : G.averageDegree = 0
  Â· have âŸ¨vâŸ© := h_nonempty
    use v
    rw [averageDegree_puncture_eq_zero_of_averageDegree_eq_zero hdâ‚€, hdâ‚€, Rat.cast_zero, F_at_zero,
    degree_eq_zero_of_averageDegree_eq_zero hdâ‚€]
    simp only [mul_one, CharP.cast_eq_zero, zero_add, add_sub_cancel, le_refl]
  Â· have hd_pos : 0 < (G.averageDegree : â„) :=
      Rat.cast_pos_of_pos (lt_of_le_of_ne averageDegree_nonneg (by rwa [ne_eq, eq_comm]))
    have cast_rw : (ğ”¼ i, (G.degree i : â„)) = G.averageDegree :=
      (algebraMap.coe_expect _ (M := â„š) _).symm
    set n := (Fintype.card V : â„) with â†hn; set d := (G.averageDegree : â„) with â†hd
    apply exists_ge_of_le_expect h_nonempty
    refine le_trans ?_
      (expect_le_expect (f := fun v â†¦ 1 + (n - (G.degree v + 1)) *
      (F d + F' d * ((G.puncture v).averageDegree - d))) ?_)
    Â· rw [â†sub_nonneg]
      suffices h : 0 â‰¤ 1 - F d * (d + 1) + F' d * (d^2 + d - n * d) +
          F' d * ğ”¼ i, ((n - ((G.degree i) + 1)) * (G.puncture i).averageDegree) by
        convert h using 1
        rw [â†sub_eq_zero]
        simp only [mul_add, add_mul, sub_mul,  expect_sub_distrib, expect_add_distrib,
        expect_const (univ_nonempty_iff.2 h_nonempty), â†expect_mul, â†mul_expect,
        mul_comm (b := F' d), â†mul_assoc (b := F' d), cast_rw, mul_sub, sub_mul]
        ring_nf; simp_rw [mul_assoc, â†mul_expect]; ring
      have h_rw : âˆ€ v, (n - (â†‘(G.degree v) + 1)) * â†‘(G.puncture v).averageDegree
        = 2 * #(G.puncture v).edgeFinset := by
        intro v
        rw [(by simp : 2 * (#((G.puncture v).edgeFinset) : â„)
          = (2 * #((G.puncture v).edgeFinset) : â„š)),
          â†card_mul_averageDegree_eq_twice_card_edges, puncture_verts_card_rat]
        simp_all
      conv => rhs; rhs; rhs; rhs; intro v; rw [h_rw]
      rw [â†mul_expect, expect_puncture_edgeFinset_card_real hT, mul_sub, mul_sub,
        (by simp : 2 * (#(G.edgeFinset) : â„) = (2 * #(G.edgeFinset) : â„š)),
        â†card_mul_averageDegree_eq_twice_card_edges, â†@F_diff_equation d, â†sub_nonneg, Rat.cast_mul,
        Rat.cast_natCast, hn, hd]
      ring_nf; rw [sub_nonneg, mul_comm (a := F' d)]; gcongr ?_ * 2
      refine mul_le_mul_of_nonpos_right ?_ (F'_nonpositive hd_pos)
      convert (Mathlib.Tactic.Rify.ratCast_le _ _).mp (averageDegree_square_bound (G := G))
      Â· simp [hd]
      Â· have := (algebraMap.coe_expect univ (M := â„š) (N := â„) (fun v â†¦ (G.degree v : â„š)^2)).symm
        simp_all
    Â· intro v _
      rw [add_le_add_iff_left]
      apply mul_le_mul_of_nonneg_left
      Â· exact F_convex_inequality (Rat.cast_nonneg.mpr averageDegree_nonneg) hd_pos
      Â· convert sub_nonneg.2 (Nat.cast_le (Î± := â„).2 (V_card_ge_deg_plus_one (G := G) (v := v)))
        rw [Nat.cast_add, Nat.cast_one]

/-- **Shearer's Theorem** [shearer1983]: The independence number of a triangle-free graph on
n vertices with average degree d is at least n Â· F(d) where F is the Shearer function. -/
theorem triangle_free_independence_bound (hT : G.CliqueFree 3)
    : G.indepNum â‰¥ (Fintype.card V) * (F G.averageDegree) := by
  suffices h : âˆ€ n, (âˆ€ {V' : Type} [Fintype V'] {G' : SimpleGraph V'} (hn : n = Fintype.card V')
    (hT : G'.CliqueFree 3), G'.indepNum â‰¥ n * (F G'.averageDegree)) from (h _ rfl hT)
  intro n
  induction' n using Nat.strong_induction_on with n hn
  intro V _ G hcard hT
  by_cases hV : Nonempty V
  Â· have âŸ¨v, hvâŸ© := exists_good_vertex hV hT
    rw [â†hcard] at hv
    refine ge_trans (ge_trans (Nat.cast_le.mpr (alpha_in_rel G v)) ?_) hv
    specialize hn _ (by convert puncture_verts_card_lt (G := G) (v := v)) rfl
      (CliqueFree.comap Ï†G hT)
    rw [puncture_verts_card, â†hcard, ge_iff_le, â†(add_le_add_iff_right (a :=1))] at hn
    convert hn using 1
    Â· rw [â†sub_eq_zero]
      simp only [hcard, Nat.cast_sub V_card_ge_deg_plus_one, Nat.cast_add, Nat.cast_one]
      ring
    Â· simp
  Â· simp_all

end SimpleGraph
