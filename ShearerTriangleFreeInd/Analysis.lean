/-
Copyright (c) 2024 Pjotr Buys. All rights reserved.
Authors: Pjotr Buys
-/

import Mathlib

/-!
# Convexity of the Shearer function.

This file analyzes the function `F : â„ â†’ â„` defined by
```
F(x) = (x * log x - x + 1) / (x - 1)Â²  for x > 0, x â‰  1
F(1) = 1/2
```

The main result is that `F` is convex on `(0, âˆ)`.

## Main definitions

* `F` : The function under study, with explicit handling of the singularity at `x = 1`.
* `F'`, `F''` : The first and second derivatives of `F`, also with explicit singularity handling.
* `S` : The domain `{x | 0 < x âˆ§ x â‰  1}` where `F` has the standard rational form.

## Main results

* `F_convex` : The function `F` is convex on `(0, âˆ)`
* `F_convex_inequality` : For `0 â‰¤ x` and `0 < y`, we have `F x â‰¥ F y + F' y * (x - y)`
* `F'_nonpositive` : For `0 < x`, we have `F' x â‰¤ 0`
* `F_diff_equation` : The funxtion `F` satisfies `1 + (x - x^2) * F' x - (x + 1) * F x = 0`.

## Implementation notes

The proof goes by defining `F'` and `F''` explicitly, showing that they are the first and second
derivative of `F`, and proving (using the explicit form of `F''`) that `F''` is nonnegative and thus
`F` convex.

## Technical notes

* The section `General` contains auxiliary lemmas, most importantly `ContinuousAt.lhopital_iterated`
  which generalizes L'HÃ´pital's rule to higher-order singularities
* The section `Calculations` handles derivatives: we use `HasDerivAt.div` for `x â‰  1` and
  the custom lemma `HasDerivAt.extend_to_singularity` for extending to `x = 1`.
-/

open Set Filter

variable {x y : â„} {f f' g g' : â„ â†’ â„} {U : Set â„}

section General

/-!
### Derivative extension lemmas
-/

-- Extension lemma for HasDerivAt
lemma HasDerivAt.congr_on_open {f'x : â„} (hU_open : IsOpen U) (hx : x âˆˆ U)
    (hf : HasDerivAt f f'x x) (heq : U.EqOn f g) : HasDerivAt g f'x x :=
  HasDerivWithinAt.hasDerivAt (HasDerivWithinAt.congr (HasDerivAt.hasDerivWithinAt hf)
    (fun _ a â†¦ (heq a).symm) (heq hx).symm) (IsOpen.mem_nhds hU_open hx)

-- Main extension theorem for derivatives
theorem HasDerivAt.extend_to_singularity (hU_open : IsOpen U) (hx : x âˆˆ U)
    (g_diff : âˆ€ y â‰  x, y âˆˆ U â†’ HasDerivAt g (g' y) y)
    (hg : ContinuousAt g x) (hg' : ContinuousAt g' x) :
    HasDerivAt g (g' x) x := by
  have hInterval : âˆƒ a b, x âˆˆ Ioo a b âˆ§ Icc a b âŠ† U := by
    have âŸ¨Î´, hÎ´, hyâŸ© :=
    Metric.eventually_nhds_iff_ball.1 (eventually_closedBall_subset (IsOpen.mem_nhds hU_open hx))
    simp_rw [Real.ball_eq_Ioo, Real.closedBall_eq_Icc] at hy
    refine âŸ¨_, _, âŸ¨mem_Ioo.mpr âŸ¨?_, ?_âŸ©, hy (Î´/2) (mem_Ioo.mpr âŸ¨?_, ?_âŸ©)âŸ©âŸ© <;> linarith
  have âŸ¨a,b, hxI ,hIUâŸ© := hInterval
  have h_diff_right : âˆ€ {y}, y âˆˆ Ioo x b â†’ HasDerivAt g (g' y) y :=
    fun hyI â†¦ (g_diff _ ((ne_of_lt hyI.1).symm)
      (hIU (mem_Icc_of_Ioo ((Ioo_subset_Ioo_left (le_of_lt hxI.1)) hyI))))
  have h_diff_left : âˆ€ {y}, y âˆˆ Ioo a x â†’ HasDerivAt g (g' y) y :=
    fun hyI â†¦ (g_diff _ (ne_of_lt hyI.2)
      (hIU (mem_Icc_of_Ioo ((Ioo_subset_Ioo_right (le_of_lt hxI.2)) hyI))))
  have h_right : HasDerivWithinAt g (g' x) (Icc x b) x := by
    rw [hasDerivWithinAt_iff_hasFDerivWithinAt]
    convert hasFDerivWithinAt_closure_of_tendsto_fderiv ?_ (convex_Ioo x b) isOpen_Ioo ?_ ?_ using 1
    Â· exact (closure_Ioo (ne_of_lt hxI.2)).symm
    Â· exact fun y hyI â†¦ DifferentiableAt.differentiableWithinAt
        (HasFDerivAt.differentiableAt (h_diff_right hyI))
    Â· rw [closure_Ioo (ne_of_lt hxI.2)]
      intro y hy
      apply ContinuousAt.continuousWithinAt
      by_cases hxy : y = x
      Â· rwa [hxy]
      Â· exact HasFDerivAt.continuousAt
          (g_diff y hxy (hIU (Icc_subset_Icc_left (le_of_lt hxI.1) hy)))
    Â· simp_rw [toSpanSingleton_deriv.symm]
      let cle := ContinuousLinearMap.toSpanSingletonCLE (ğ•œ := â„) (E := â„)
      exact Tendsto.comp (cle.continuous.continuousAt)
        (Tendsto.congr' (eventuallyEq_nhdsWithin_of_eqOn
          (fun _ hyI â†¦ (HasDerivAt.deriv (h_diff_right hyI)).symm))
        (Tendsto.mono_left (y := nhdsWithin x (Ioo x b)) hg' nhdsWithin_le_nhds))
  have h_left : HasDerivWithinAt g (g' x) (Icc a x) x := by
    rw [hasDerivWithinAt_iff_hasFDerivWithinAt]
    convert hasFDerivWithinAt_closure_of_tendsto_fderiv ?_ (convex_Ioo a x) isOpen_Ioo ?_ ?_ using 1
    Â· exact (closure_Ioo (ne_of_lt hxI.1)).symm
    Â· exact fun y hyI â†¦ DifferentiableAt.differentiableWithinAt
        (HasFDerivAt.differentiableAt (h_diff_left hyI))
    Â· rw [closure_Ioo (ne_of_lt hxI.1)]
      intro y hy
      apply ContinuousAt.continuousWithinAt
      by_cases hxy : y = x
      Â· rwa [hxy]
      Â· exact HasFDerivAt.continuousAt (g_diff y hxy
          (hIU (Icc_subset_Icc_right (le_of_lt hxI.2) hy)))
    Â· simp_rw [toSpanSingleton_deriv.symm]
      let cle := ContinuousLinearMap.toSpanSingletonCLE (ğ•œ := â„) (E := â„)
      exact Tendsto.comp (cle.continuous.continuousAt)
        (Tendsto.congr' (eventuallyEq_nhdsWithin_of_eqOn
          (fun _ hyI â†¦ (HasDerivAt.deriv (h_diff_left hyI)).symm))
        (Tendsto.mono_left (y := nhdsWithin x (Ioo a x)) hg' nhdsWithin_le_nhds))
  convert HasDerivWithinAt.hasDerivAt (HasDerivWithinAt.mono (h_left.union h_right) ?_)
    (Ioo_mem_nhds hxI.1 hxI.2)
  exact fun y hy â†¦ (le_or_gt x y).elim (fun hxy => Or.inr âŸ¨hxy, hy.2.leâŸ©)
    (fun hxy => Or.inl âŸ¨hy.1.le, hxy.leâŸ©)


/-!
### Auxiliary analysis lemmas
-/

lemma pos_of_dist_one_lt_one (hx : dist x 1 < 1) : 0 < x := by
  rw [Real.dist_eq, abs_sub_lt_iff] at hx
  linarith

-- Extract property from neighborhood
lemma eventually_nhds_self {p : â„ â†’ Prop} (h : âˆ€á¶  (y : â„) in nhds x, p y) : p x := by
  have âŸ¨_, hÎµ, hallâŸ© := (Metric.eventually_nhds_iff_ball.1 h)
  exact hall _ (Metric.mem_ball_self hÎµ)

-- Propagate zeros to derivatives
lemma frequently_deriv_eq_zero_of_frequently_eq_zero
    (hx : g x = 0)
    (hDer : âˆ€á¶  (y : â„) in nhds x, HasDerivAt g (g' y) y)
    (hgZero : âˆƒá¶  (y : â„) in nhdsWithin x {x}á¶œ, g y = 0) :
    âˆƒá¶  (y : â„) in nhdsWithin x {x}á¶œ, g' y = 0 := by
  rw [frequently_nhdsWithin_iff, frequently_nhds_iff]
  intro U hxU hUopen
  rw [eventually_nhds_iff] at hDer
  have âŸ¨V, hVder, hVopen, hxVâŸ© := hDer
  have âŸ¨Îµ, hÎµ, hBallâŸ© := Metric.isOpen_iff.1 (hUopen.inter hVopen) _ âŸ¨hxU, hxVâŸ©
  rw [frequently_nhdsWithin_iff, frequently_nhds_iff] at hgZero
  have âŸ¨y, hUy, hy, hynxâŸ© :=  hgZero (Metric.ball x Îµ) (Metric.mem_ball_self hÎµ) Metric.isOpen_ball
  rw [Set.mem_compl_singleton_iff] at hynx
  let a := min x y; let b := max x y
  have rw_cases : a = x âˆ§ b = y âˆ¨ a = y âˆ§ b = x := by
    rcases (lt_or_gt_of_ne hynx) with hyx | hxy
    Â· right; exact âŸ¨min_eq_right_of_lt hyx, max_eq_left_of_lt hyxâŸ©
    Â· left; exact âŸ¨min_eq_left_of_lt hxy, max_eq_right_of_lt hxyâŸ©
  have hab : a < b := min_lt_max.2 hynx.symm
  have hsub : Set.Icc a b âŠ† U âˆ© V := by
    refine subset_trans ?_ hBall
    refine IsConnected.Icc_subset (Metric.isConnected_ball hÎµ) ?_ ?_
    Â· rcases rw_cases with h | h <;> rw[h.1]
      Â· exact Metric.mem_ball_self hÎµ
      Â· exact hUy
    Â· rcases rw_cases with h | h <;> rw[h.2]
      Â· exact hUy
      Â· exact Metric.mem_ball_self hÎµ
  have hgab : g a = 0 âˆ§ g b = 0 := by
    rcases rw_cases with h | h <;> rw[h.1, h.2]
    Â· exact âŸ¨hx, hyâŸ©
    Â· exact âŸ¨hy, hxâŸ©
  have hxnot : x âˆ‰ Set.Ioo a b := by
    intro hcontra; rw [Set.mem_Ioo] at hcontra
    rcases rw_cases with h | h <;> (rw[h.1, h.2] at hcontra; linarith)
  have âŸ¨z,hz,hg'zâŸ© := exists_hasDerivAt_eq_slope g g' hab ?_ ?_
  Â· rw [hgab.1, hgab.2, sub_self, zero_div] at hg'z
    use z, (hsub (Set.Ioo_subset_Icc_self hz)).1, hg'z
    intro hcontra
    rw [Set.mem_singleton_iff.1 hcontra] at hz
    exact hxnot hz
  Â· exact HasDerivAt.continuousOn (fun w hw  â†¦ hVder _ (hsub hw).2)
  Â· exact (fun w hw  â†¦ hVder _ (hsub (Set.Ioo_subset_Icc_self hw)).2)

lemma ContinuousAt.eq_of_frequently_eq {g : â„ â†’ â„} {a x : â„}
    (hZero : âˆƒá¶  y in nhdsWithin x {x}á¶œ, g y = a)
    (hCont : ContinuousAt g x) :
    g x = a :=
    (tendsto_nhds_unique_of_frequently_eq
      (tendsto_nhdsWithin_of_tendsto_nhds hCont)
      tendsto_const_nhds hZero)



lemma ConvexOn.tangent_line_le (hgc : ConvexOn â„ U g) (hx : x âˆˆ U) (hy : y âˆˆ U)
  (hg' : HasDerivAt g (g' y) y) : g y + g' y * (x - y) â‰¤ g x := by
  rcases lt_trichotomy x y with hxy | hxy | hxy
  Â· rw [â†le_sub_iff_add_le', â†neg_le_neg_iff, neg_sub, mul_comm, neg_mul_eq_neg_mul, neg_sub,
          â†div_le_iffâ‚€' (by linarith), div_eq_inv_mul]
    convert ConvexOn.slope_le_of_hasDerivAt hgc hx hy hxy hg' using 1
  Â· simp only [hxy, sub_self, mul_zero, add_zero, le_refl]
  Â· let Ï† : â„ â†’ â„   := fun x â†¦ -x
    have hgmc   : ConvexOn â„ (Ï† '' U) (g âˆ˜ Ï†) := by
      refine âŸ¨Convex.is_linear_image hgc.1 (IsLinearMap.isLinearMap_neg), ?_âŸ©
      intro mv âŸ¨v, hvS, hvâŸ© mw âŸ¨w, hwS, hwâŸ© a b hap hbp hab
      rw [â†hv, â†hw]
      convert hgc.2 hvS hwS hap hbp hab using 1 <;> unfold Ï†
      Â· simp only [Function.comp_apply, smul_eq_mul, mul_neg, neg_add, neg_neg]
      Â· simp only [Function.comp_apply, neg_neg, smul_eq_mul]
    have hgm'c  : HasDerivAt (g âˆ˜ Ï†) (Ï† (g' y)) (Ï† y) := by
      convert HasDerivAt.scomp (Ï† y) (h := Ï†) (h' := -1) (gâ‚ := g) (gâ‚' := g' y) ?_ ?_ using 1
      Â· simp only [smul_eq_mul, neg_mul, one_mul, Ï†]
      Â· convert hg'
        simp [Ï†]
      Â· exact hasDerivAt_neg' _
    rw [â†le_sub_iff_add_le]
    apply (tsub_le_iff_left (c := - (g' y * (x - y)))).1
    rw [neg_mul_eq_neg_mul, â†mul_inv_le_iffâ‚€ (by linarith), mul_comm]
    convert ConvexOn.slope_le_of_hasDerivAt (x := -x) hgmc ?_ ?_ ?_ hgm'c using 1
    Â· rw [slope, sub_neg_eq_add, add_comm]
      congr 1
      simp [Ï†]
    Â· exact (Set.mem_image _ _ _).2 âŸ¨x, hx, rflâŸ©
    Â· exact (Set.mem_image _ _ _).2 âŸ¨y, hy, rflâŸ©
    Â· exact neg_lt_neg_iff.mpr hxy

/-!
### L'HÃ´pital's rule
-/

-- General iterated L'HÃ´pital's rule
lemma ContinuousAt.lhopital_iterated (n : â„•) (ns ds : List (â„ â†’ â„))
    (hnslen : ns.length = n + 1) (hdslen : ds.length = n + 1)
    (hnDer : âˆ€ k (hk : k < n), âˆ€á¶  (y : â„) in nhds x, HasDerivAt ns[k] (ns[k + 1] y) y)
    (hdDer : âˆ€ k (hk : k < n), âˆ€á¶  (y : â„) in nhds x, HasDerivAt ds[k] (ds[k + 1] y) y)
    (hnZero : âˆ€ k (hk : k < n), ns[k] x = 0)
    (hdZero : âˆ€ k (hk : k < n), ds[k] x = 0)
    (hnCont : ContinuousAt ns[n] x)
    (hdCont : ContinuousAt ds[n] x)
    (hdFinal : ds[n] x â‰  0) :
    ContinuousAt
        (fun y â†¦ if y = x
          then (ns[n] x) / (ds[n] x)
          else (ns[0] y) / (ds[0] y))
        x := by
  revert ns ds n; intro n
  induction n with
  | zero =>
    intro ns ds _ _ _ _ _ _  hnC hdC hnZ
    refine ContinuousAt.congr (ContinuousAt.divâ‚€ hnC hdC hnZ) (Eq.eventuallyEq ?_)
    ext; split <;> simp_all only [ne_eq]
  | succ n ih =>
    intro ns ds len_ns len_ds hnDer hdDer hnZero hdZero hnC hdC hnZ
    rw [â†continuousWithinAt_compl_self, ContinuousWithinAt, if_pos rfl]
    apply Filter.Tendsto.congr' (fâ‚ := fun y â†¦ (ns[0] y) / (ds[0] y) )
      (eventually_nhdsWithin_of_forall fun y hy â†¦ (if_neg hy).symm)
    have hn : 0 < n + 1 := Nat.zero_lt_succ _
    have hn_tail : ns.tail.length = n + 1 := by
      rw [List.length_tail, len_ns, add_tsub_cancel_right, Nat.add_left_cancel_iff]
    have hd_tail : ds.tail.length = n + 1 := by
      rw [List.length_tail, len_ds, add_tsub_cancel_right, Nat.add_left_cancel_iff]
    refine HasDerivAt.lhopital_zero_nhdsNE (f' := ns[1]) (g' := ds[1]) ?_ ?_ ?_ ?_ ?_ ?_
    Â· exact eventually_nhdsWithin_of_eventually_nhds (hnDer 0 hn)
    Â· exact eventually_nhdsWithin_of_eventually_nhds (hdDer 0 hn)
    Â· by_contra hcontra
      simp_rw [Filter.not_eventually, not_ne_iff] at hcontra
      have hInd : âˆ€ k (hk : k < n + 1), âˆƒá¶  (x : â„) in nhdsWithin x {x}á¶œ, ds[k+1] x = 0 := by
        intro k
        induction k with
        | zero => exact fun _ â†¦ hcontra
        | succ _ ih =>
          exact fun _ â†¦ frequently_deriv_eq_zero_of_frequently_eq_zero (hdZero _ (by linarith))
            (hdDer _ (by linarith)) (ih (by linarith))
      specialize hInd n (by linarith)
      exact hnZ (ContinuousAt.eq_of_frequently_eq hInd hdC)
    Â· rw [â†(hnZero 0 hn)]
      refine continuousWithinAt_compl_self.2 ?_
      have this := (eventually_nhds_self (hnDer 0 hn))
      exact HasDerivAt.continuousAt this
    Â· rw [â†(hdZero 0 hn)]
      refine continuousWithinAt_compl_self.2 ?_
      have this := (eventually_nhds_self (hdDer 0 hn))
      exact HasDerivAt.continuousAt this
    Â· specialize ih ns.tail ds.tail hn_tail hd_tail ?_ ?_ ?_ ?_ ?_ ?_ ?_
      Â· intro k hk
        convert hnDer (k+1) (by linarith) using 3
        Â· refine List.getElem_tail (by linarith)
        Â· simp
      Â· intro k hk
        convert hdDer (k+1) (by linarith) using 3
        Â· refine List.getElem_tail (by linarith)
        Â· simp
      Â· exact fun k hk â†¦ by convert hnZero (k + 1) (by linarith) using 1; simp
      Â· exact fun k hk â†¦ by convert hdZero (k + 1) (by linarith) using 1; simp
      Â· convert hnC using 1; simp
      Â· convert hdC using 1; simp
      Â· convert hnZ using 1; simp
      Â· rw [â†continuousWithinAt_compl_self, ContinuousWithinAt, if_pos rfl] at ih
        convert Filter.Tendsto.congr' ?_ ih using 2
        Â· simp
        Â· refine eventually_nhdsWithin_of_forall ?_
          intro _ hy
          convert if_neg hy using 1
          simp

end General


/-!
### Function analysis and calculations
-/

noncomputable section Calculations

open Polynomial

/-!
### Main definitions
-/

-- Domain
def S : Set â„ := {x | 0 < x âˆ§ x â‰  1}

-- Main function `F` and its derivatives.
def F : â„ â†’ â„ := fun x â†¦
  if x = 1 then 1/2
  else          (x * Real.log x - x + 1) / (x - 1)^2

def F' : â„ â†’ â„ := fun x â†¦
  if x = 1 then -1/6
  else          (-2 + 2*x - Real.log x - x * Real.log x) / (x - 1)^3

def F'' : â„ â†’ â„ := fun x â†¦
  if x = 1 then 1/6
  else          (1 + 4*x - 5*x^2 + 4 * x * Real.log x + 2 * x^2 * Real.log x) / ((x - 1)^4 * x)

/-!
### Numerators and denominators of F, F', and F''.
-/
-- `F`
def nFâ‚€ : â„ â†’ â„ := fun x â†¦ x * Real.log x - x + 1
def nFâ‚ : â„ â†’ â„ := fun x â†¦ Real.log x
def nFâ‚‚ : â„ â†’ â„ := fun x â†¦ 1/x

def dFâ‚€ : â„ â†’ â„ := fun x â†¦ (x - 1)^2
def dFâ‚ : â„ â†’ â„ := fun x â†¦ 2 * (x - 1)
def dFâ‚‚ : â„ â†’ â„ := fun _ â†¦ 2

-- `F'`
def nF'â‚€ : â„ â†’ â„ := fun x â†¦ -2 + 2*x - Real.log x - x * Real.log x
def nF'â‚ : â„ â†’ â„ := fun x â†¦ 1 - 1/x - Real.log x
def nF'â‚‚ : â„ â†’ â„ := fun x â†¦ 1 / x^2 - 1 / x
def nF'â‚ƒ : â„ â†’ â„ := fun x â†¦ -2 / x^3 + 1 / x^2

def dF'â‚€ : â„ â†’ â„ := fun x â†¦ (x - 1)^3
def dF'â‚ : â„ â†’ â„ := fun x â†¦ 3 * (x - 1)^2
def dF'â‚‚ : â„ â†’ â„ := fun x â†¦ 6 * (x - 1)
def dF'â‚ƒ : â„ â†’ â„ := fun _ â†¦ 6

-- `F''`
def nF''â‚€ : â„ â†’ â„ := fun x â†¦ 1 + 4*x - 5*x^2 + 4 * x * Real.log x + 2 * x^2 * Real.log x
def nF''â‚ : â„ â†’ â„ := fun x â†¦ 8 - 8 * x + 4 * Real.log x  + 4 * x * Real.log x
def nF''â‚‚ : â„ â†’ â„ := fun x â†¦ -4 + 4/x + 4 * Real.log x
def nF''â‚ƒ : â„ â†’ â„ := fun x â†¦ -4 / x^2 + 4 / x
def nF''â‚„ : â„ â†’ â„ := fun x â†¦ 8 / x^3 - 4 / x^2

def dF''â‚€ : â„ â†’ â„ := fun x â†¦ (x - 1)^4 * x
def dF''â‚ : â„ â†’ â„ := fun x â†¦ 1 - 8 * x + 18 * x^2 - 16 * x^3 + 5 * x^4
def dF''â‚‚ : â„ â†’ â„ := fun x â†¦ -8 + 36 * x - 48 * x^2 + 20 * x^3
def dF''â‚ƒ : â„ â†’ â„ := fun x â†¦ 36 - 96 * x + 60 * x^2
def dF''â‚„ : â„ â†’ â„ := fun x â†¦ -96 + 120 * x

/-!
### Lemmas about S
-/

lemma S_open : IsOpen S := IsOpen.and (isOpen_lt' 0) (isOpen_ne)

lemma pos_not_in_S_eq_one (hx_pos : 0 < x) (hx_not_in_S : x âˆ‰ S) : x = 1 := by
  by_contra hxn1
  exact hx_not_in_S âŸ¨hx_pos, hxn1âŸ©

/-!
### F, F', and F'' equal to rational functions on S
-/


lemma F_not_one (hx : x â‰  1) : F x   = nFâ‚€ x / dFâ‚€ x       := if_neg hx
lemma F'_not_one (hx : x â‰  1) : F' x  = nF'â‚€ x / dF'â‚€ x     := if_neg hx
lemma F''_not_one (hx : x â‰  1) : F'' x = nF''â‚€ x / dF''â‚€ x   := if_neg hx

lemma F_at_zero : F 0    = 1  := by simp [F]
lemma F_at_one : F 1    = 1/2  := if_pos rfl
lemma F'_at_one : F' 1   = -1/6 := if_pos rfl
lemma F''_at_one : F'' 1  = 1/6  := if_pos rfl

lemma F_on_S : S.EqOn F    (fun x â†¦ nFâ‚€ x    / dFâ‚€ x)    := fun _ hx â†¦ if_neg hx.2
lemma F'_on_S : S.EqOn F'   (fun x â†¦ nF'â‚€ x   / dF'â‚€ x)   := fun _ hx â†¦ if_neg hx.2
lemma F''_on_S : S.EqOn F''  (fun x â†¦ nF''â‚€ x  / dF''â‚€ x)  := fun _ hx â†¦ if_neg hx.2

/-!
### Derivatives of the numerators and denominators of F, F' and F''
-/

/-!
  Two helpful rewrite lemmas about the derivatives of `x â†¦ 1/x` and `x â†¦ 1/x^2`.
-/

lemma hasDerivAtInv (hx : 0 < x) : HasDerivAt (fun y â†¦ 1 / y) (- 1 / x^2) x := by
  convert hasDerivAt_inv (ne_of_lt hx).symm using 1
  Â· ext _; ring
  Â· ring

lemma hasDerivAtInvSq (hx : 0 < x) : HasDerivAt (fun y â†¦ 1 / y^2) (- 2 / x^3) x := by
  convert HasDerivAt.inv (c := fun y â†¦ y^2) (c' := 2 * x) ?_ ?_ using 1
  Â· ext _; simp
  Â· field_simp
  Â· convert Polynomial.hasDerivAt (X (R := â„) ^ 2) _ <;> norm_num
  Â· positivity

-- `F`

lemma hasDerivAt_nFâ‚€ (hx : 0 < x) : HasDerivAt nFâ‚€ (nFâ‚ x) x := by
  unfold nFâ‚€ nFâ‚
  convert HasDerivAt.add_const 1 (HasDerivAt.sub (f := fun x â†¦ x * Real.log x)
    (f' := Real.log x + 1) ?_ (hasDerivAt_id' x)) using 1
  Â· ring
  Â· convert HasDerivAt.mul (hasDerivAt_id' x) (Real.hasDerivAt_log (ne_of_lt hx).symm) using 1
    field_simp

lemma hasDerivAt_nFâ‚ (hx : 0 < x) : HasDerivAt nFâ‚ (nFâ‚‚ x) x := by
  unfold nFâ‚‚
  convert Real.hasDerivAt_log (ne_of_lt hx).symm using 1
  exact one_div x

lemma hasDerivAt_dFâ‚€ : HasDerivAt dFâ‚€ (dFâ‚ x) x := by
  convert Polynomial.hasDerivAt (1 - 2 * X (R := â„) + X^2) _ <;> norm_num
  Â· unfold dFâ‚€; ring
  Â· unfold dFâ‚; ring

lemma hasDerivAt_dFâ‚ : HasDerivAt dFâ‚ (dFâ‚‚ x) x := by
  convert Polynomial.hasDerivAt (-2 + 2 * X (R := â„)) _ <;> norm_num
  Â· unfold dFâ‚; ring
  Â· rfl

-- `F'`

lemma hasDerivAt_nF'â‚€ (hx : 0 < x) : HasDerivAt nF'â‚€ (nF'â‚ x) x := by
  unfold nF'â‚€ nF'â‚
  convert HasDerivAt.sub (f := fun x â†¦ 2*x - 2) (f' := 2)
    (g := fun x â†¦ (1 + x) * Real.log x) (g' := 1 + 1/x + Real.log x) ?_ ?_ using 1
  Â· ext _; rw [Pi.sub_apply]; ring
  Â· ring
  Â· convert Polynomial.hasDerivAt (- 2 + 2 * X (R := â„)) _ <;> norm_num
    ring
  Â· convert HasDerivAt.mul (c := fun x â†¦ 1 + x) (c' := 1) ?_
      (Real.hasDerivAt_log (ne_of_lt hx).symm) using 1
    Â· field_simp; ring
    Â· convert Polynomial.hasDerivAt (1 + X (R := â„)) _ <;> norm_num

lemma hasDerivAt_nF'â‚ (hx : 0 < x) : HasDerivAt nF'â‚ (nF'â‚‚ x) x := by
  unfold nF'â‚ nF'â‚‚
  convert HasDerivAt.sub (f := fun x â†¦ 1 - 1/x) (f' := 1 / x^2) ?_
    (Real.hasDerivAt_log (ne_of_lt hx).symm) using 1
  Â· field_simp
  Â· convert HasDerivAt.sub (g := fun x â†¦ 1 / x) (g' := - 1 / x^2)
      (hasDerivAt_const x 1) (hasDerivAtInv hx) using 1
    ring

lemma hasDerivAt_nF'â‚‚ (hx : 0 < x) : HasDerivAt nF'â‚‚ (nF'â‚ƒ x) x := by
  unfold nF'â‚‚ nF'â‚ƒ
  convert HasDerivAt.sub (hasDerivAtInvSq hx) (hasDerivAtInv hx) using 1
  field_simp; ring

lemma hasDerivAt_dF'â‚€ : HasDerivAt dF'â‚€ (dF'â‚ x) x := by
  convert Polynomial.hasDerivAt (- 1 + 3 * X (R := â„) - 3 * X^2 + X^3) _ <;> norm_num
  Â· unfold dF'â‚€; ring
  Â· unfold dF'â‚; ring

lemma hasDerivAt_dF'â‚ : HasDerivAt dF'â‚ (dF'â‚‚ x) x := by
  convert Polynomial.hasDerivAt (3 - 6 * X (R := â„) + 3 * X^2) _ <;> norm_num
  Â· unfold dF'â‚; ring
  Â· unfold dF'â‚‚; ring

lemma hasDerivAt_dF'â‚‚ : HasDerivAt dF'â‚‚ (dF'â‚ƒ x) x := by
  convert Polynomial.hasDerivAt (-6 + 6 * X (R := â„)) _ <;> norm_num
  Â· unfold dF'â‚‚; ring
  Â· unfold dF'â‚ƒ; ring

-- `F''`

lemma hasDerivAt_nF''â‚€ (hx : 0 < x) : HasDerivAt nF''â‚€ (nF''â‚ x) x := by
  unfold nF''â‚€ nF''â‚
  convert HasDerivAt.add
    (f := fun x â†¦ 1 + 4 * x - 5 * x^2) (f' := 4 - 10*x)
    (g := fun x â†¦ (4 * x + 2 * x^2) * Real.log x)
    (g' := 4 + 2 * x + 4 * Real.log x + 4 * x * Real.log x) ?_ ?_ using 1
  Â· ext _; rw [Pi.add_apply]; ring
  Â· ring
  Â· convert Polynomial.hasDerivAt (1 + 4 * X (R := â„) - 5 * X^2) _ <;> norm_num; ring
  Â· convert HasDerivAt.mul (c := fun x â†¦ 4 * x + 2 * x^2) (c' := 4 + 4 * x) ?_
      (Real.hasDerivAt_log (ne_of_lt hx).symm) using 1
    Â· field_simp; ring
    Â· convert Polynomial.hasDerivAt (4 * X (R := â„) + 2 * X^2) _ <;> norm_num; ring

lemma hasDerivAt_nF''â‚ (hx : 0 < x) : HasDerivAt nF''â‚ (nF''â‚‚ x) x := by
  unfold nF''â‚ nF''â‚‚
  convert HasDerivAt.add
    (f := fun x â†¦ 8 - 8 * x) (f' := - 8)
    (g := fun x â†¦ (4 + 4 * x) * Real.log x) (g' := 4 + 4 / x + 4 * Real.log x) ?_ ?_ using 1
  Â· ext _; rw [Pi.add_apply]; ring
  Â· ring
  Â· convert Polynomial.hasDerivAt (8 - 8 * X (R := â„)) _ <;> norm_num
  Â· convert HasDerivAt.mul (c := fun x â†¦ 4 + 4 * x) (c' := 4) ?_
      (Real.hasDerivAt_log (ne_of_lt hx).symm) using 1
    Â· field_simp; ring
    Â· convert Polynomial.hasDerivAt (4 + 4 * X (R := â„)) _ <;> norm_num

lemma hasDerivAt_nF''â‚‚ (hx : 0 < x) : HasDerivAt nF''â‚‚ (nF''â‚ƒ x) x := by
  unfold nF''â‚‚ nF''â‚ƒ
  convert HasDerivAt.add
    (f := fun x â†¦ -4 + 4/x) (f' := -4 / x^2) ?_
    (HasDerivAt.const_mul _ (Real.hasDerivAt_log (ne_of_lt hx).symm)) using 1
  convert HasDerivAt.add (hasDerivAt_const x (-4))
    (HasDerivAt.const_mul 4 (hasDerivAtInv hx)) using 1
  Â· ext _; rw [Pi.add_apply]; ring
  Â· ring

lemma hasDerivAt_nF''â‚ƒ (hx : 0 < x) : HasDerivAt nF''â‚ƒ (nF''â‚„ x) x := by
  unfold nF''â‚ƒ nF''â‚„
  convert HasDerivAt.add (HasDerivAt.const_mul (-4) (hasDerivAtInvSq hx))
    (HasDerivAt.const_mul 4 (hasDerivAtInv hx)) using 1
  Â· ext _; rw [Pi.add_apply]; ring
  Â· ring

lemma hasDerivAt_dF''â‚€ : HasDerivAt dF''â‚€ (dF''â‚ x) x := by
  convert Polynomial.hasDerivAt (X - 4 * X^2 + 6 * X^3 - 4 * X^4 + X^5 : â„[X]) _ <;> norm_num
  Â· unfold dF''â‚€; ring
  Â· unfold dF''â‚; ring

lemma hasDerivAt_dF''â‚ : HasDerivAt dF''â‚ (dF''â‚‚ x) x := by
  convert Polynomial.hasDerivAt (1 - 8 * X (R := â„) + 18 * X^2 - 16 * X^3 + 5 * X^4) _ <;> norm_num
  Â· unfold dF''â‚; ring
  Â· unfold dF''â‚‚; ring

lemma hasDerivAt_dF''â‚‚ : HasDerivAt dF''â‚‚ (dF''â‚ƒ x) x := by
  convert Polynomial.hasDerivAt (-8 + 36 * X (R := â„) - 48 * X^2 + 20 * X^3 ) _ <;> norm_num
  Â· unfold dF''â‚‚; ring
  Â· unfold dF''â‚ƒ; ring

lemma hasDerivAt_dF''â‚ƒ : HasDerivAt dF''â‚ƒ (dF''â‚„ x) x := by
  convert Polynomial.hasDerivAt (36 - 96 * X (R := â„) + 60 * X^2) _ <;> norm_num
  Â· unfold dF''â‚ƒ; ring
  Â· unfold dF''â‚„; ring




/-!
### Continuity of (derivatives of) denominators and numerators
-/

lemma ContinuousAt_dFâ‚€ : ContinuousAt dFâ‚€ x := by
  unfold dFâ‚€
  exact ContinuousAt.pow (ContinuousAt.sub (by exact fun â¦ƒUâ¦„ a â†¦ a) continuousAt_const) _

lemma ContinuousAt_nFâ‚€ : ContinuousAt nFâ‚€ x  :=
  ContinuousAt.add (ContinuousAt.sub (Continuous.continuousAt Real.continuous_mul_log ) fun _ a â†¦ a)
    continuousAt_const

lemma ContinuousAt_nFâ‚‚ (hx : 0 < x) : ContinuousAt nFâ‚‚ x := by unfold nFâ‚‚; continuity

lemma ContinuousAt_dFâ‚‚ : ContinuousAt dFâ‚‚ x := continuousAt_const

lemma ContinuousAt_nF'â‚ƒ (hx : 0 < x) : ContinuousAt nF'â‚ƒ x := by
  refine ContinuousAt.add ?_ ?_ <;>
  exact ContinuousAt.divâ‚€ continuousAt_const
    (ContinuousAt.pow (by exact fun _ a â†¦ a) _) (by positivity)

lemma ContinuousAt_dF'â‚ƒ : ContinuousAt dF'â‚ƒ x := continuousAt_const

lemma ContinuousAt_nF''â‚„ (hx : 0 < x) : ContinuousAt nF''â‚„ x := by
  refine ContinuousAt.add ?_ (ContinuousAt.neg ?_) <;>
  exact ContinuousAt.divâ‚€ continuousAt_const
    (ContinuousAt.pow (by exact fun _ a â†¦ a) _) (by positivity)

lemma ContinuousAt_dF''â‚„ : ContinuousAt dF''â‚„ x := by
  unfold dF''â‚„
  exact Continuous.continuousAt (by continuity)


/-!
### Useful (in)equalities involving the denominators and numerators
-/



lemma dFâ‚€_ne_zero (hx : x â‰  1) : dFâ‚€ x â‰  0      := pow_ne_zero 2 (sub_ne_zero_of_ne hx)
lemma dFâ‚€_ne_zero_on_S (hx : x âˆˆ S) : dFâ‚€ x â‰  0 := dFâ‚€_ne_zero hx.2

lemma dF'â‚€_ne_zero (hx : x â‰  1) : dF'â‚€ x â‰  0      := pow_ne_zero 3 (sub_ne_zero_of_ne hx)
lemma dF'â‚€_ne_zero_on_S (hx : x âˆˆ S) : dF'â‚€ x â‰  0 := dF'â‚€_ne_zero hx.2
lemma dF'â‚€_nonneg (hx : 1 < x) : 0 â‰¤ dF'â‚€ x       := pow_nonneg (by linarith) 3

lemma dF''â‚€_nonneg (hx : 0 < x) : 0 â‰¤ dF''â‚€ x  := by unfold dF''â‚€; positivity

lemma dF''â‚€_ne_zero_on_S (hx : x âˆˆ S) : dF''â‚€ x â‰  0   :=
  (mul_ne_zero_iff_right (ne_of_lt hx.1).symm).mpr (pow_ne_zero 4 (sub_ne_zero_of_ne hx.2))


/-!
  To prove that `nF''â‚€` is nonnegative we define two types of functions:
  `isType1`: Nonincreasing on (0,1], nondecreasing on [1,âˆ) and zero at x = 1.
  `isType2`: Nonpositive on (0,1] and nonnegative on [1,âˆ).
  We prove that a function `l` with `l 1 = 0` whose derivative is one type is itself the other type.
-/

def isType1 (l : â„ â†’ â„) :=  AntitoneOn l (Ioc 0 1) âˆ§ MonotoneOn l (Ici 1) âˆ§ (l 1 = 0)
def isType2 (l : â„ â†’ â„) :=  (âˆ€ {x}, x âˆˆ Ioc 0 1 â†’ l x â‰¤ 0) âˆ§ (âˆ€ {x}, x âˆˆ Ici 1 â†’ 0 â‰¤ l x)

lemma Type1_nonneg {l : â„ â†’ â„} (hType : isType1 l) (hx : 0 < x) : 0 â‰¤ l x := by
  rw [â†hType.2.2]
  rcases le_or_gt x 1 with hx1 | hx1
  Â· exact hType.1 âŸ¨hx,hx1âŸ© âŸ¨Real.zero_lt_one, le_refl 1âŸ© hx1
  Â· exact hType.2.1 self_mem_Ici (mem_Ici_of_Ioi hx1) (le_of_lt hx1)

lemma Type2_mul_nonneg {l m : â„ â†’ â„} (hType : isType2 l) (hm : âˆ€ {x}, 0 < x â†’ 0 â‰¤ m x) :
  isType2 (fun x â†¦ l x * m x) :=
  âŸ¨ fun hx â†¦ mul_nonpos_of_nonpos_of_nonneg (hType.1 hx) (hm hx.1),
    fun hx â†¦ Left.mul_nonneg (hType.2 hx) (hm (by linarith [mem_Ici.1 hx]))âŸ©

lemma DerivAt_Type1_imp_Type2 {l l' : â„ â†’ â„} (hType : isType1 l')
    (hDer : âˆ€ {x}, 0 < x â†’ HasDerivAt l (l' x) x)
    (h : l 1 = 0) : isType2 l := by
  have h_mon : MonotoneOn l (Ioi 0) := by
    apply monotoneOn_of_hasDerivWithinAt_nonneg (f' := l')
    Â· exact fun _ hx â†¦ ContinuousAt.continuousWithinAt (HasDerivAt.continuousAt (hDer hx))
    Â· rw [interior_Ioi]
      exact fun y hy â†¦ HasDerivAt.hasDerivWithinAt (hDer hy)
    Â· rw [interior_Ioi]
      exact fun x hx â†¦ Type1_nonneg hType hx
    Â· exact convex_Ioi 0
  refine âŸ¨?_,?_âŸ© <;> (intro x hx; rw [â†h])
  Â· exact h_mon (mem_of_mem_inter_left hx) (by norm_num) (by exact hx.2)
  Â· refine h_mon (by norm_num) ?_ (by exact hx)
    rw [mem_Ioi]; rw [mem_Ici] at hx; linarith

lemma DerivAt_Type2_imp_Type1 {l l' : â„ â†’ â„} (hType : isType2 l')
    (hDer : âˆ€ {x}, 0 < x â†’ HasDerivAt l (l' x) x)
    (h : l 1 = 0) : isType1 l := by
  refine âŸ¨?_, âŸ¨?_, hâŸ©âŸ©
  Â· apply antitoneOn_of_hasDerivWithinAt_nonpos (f' := l')
    Â· exact fun _ hx â†¦ ContinuousAt.continuousWithinAt (HasDerivAt.continuousAt (hDer hx.1))
    Â· rw [interior_Ioc]
      exact fun y hy â†¦ HasDerivAt.hasDerivWithinAt (hDer hy.1)
    Â· rw [interior_Ioc]
      exact fun x hx â†¦ (hType.1 (mem_Ioc_of_Ioo hx))
    Â· exact convex_Ioc 0 1
  Â· apply monotoneOn_of_hasDerivWithinAt_nonneg (f' := l')
    Â· exact fun _ hx â†¦ ContinuousAt.continuousWithinAt
        (HasDerivAt.continuousAt (hDer (by linarith [mem_Ici.1 hx])))
    Â· rw [interior_Ici]
      exact fun y hy â†¦ HasDerivAt.hasDerivWithinAt (hDer (by linarith [mem_Ioi.1 hy]))
    Â· rw [interior_Ici]
      exact fun x hx â†¦ (hType.2 (mem_Ici_of_Ioi hx))
    Â· exact convex_Ici 1


/-!
  We prove that the first few derivatives of `nF''â‚€` alternate between these two types.
-/

lemma isType2_nF''â‚ƒ : isType2 nF''â‚ƒ := by
  convert Type2_mul_nonneg (l := fun x â†¦ -4 + 4*x) (m := fun x â†¦ 1 / x^2) ?_ ?_ using 1
  Â· unfold nF''â‚ƒ; ext x;
    by_cases hx : x = 0
    Â· rw [hx]; simp
    Â· field_simp
  Â· refine âŸ¨?_,?_âŸ© <;> (intro _ _; simp_all)
  Â· intro _ _; simp [sq_nonneg]

lemma isType1_nF''â‚‚ : isType1 nF''â‚‚ :=
  DerivAt_Type2_imp_Type1 isType2_nF''â‚ƒ hasDerivAt_nF''â‚‚ (by norm_num [nF''â‚‚])

lemma isType2_nF''â‚ : isType2 nF''â‚ :=
  DerivAt_Type1_imp_Type2 isType1_nF''â‚‚ hasDerivAt_nF''â‚ (by norm_num [nF''â‚])

lemma isType1_nF''â‚€ : isType1 nF''â‚€ :=
  DerivAt_Type2_imp_Type1 isType2_nF''â‚ hasDerivAt_nF''â‚€ (by norm_num [nF''â‚€])

lemma nF''â‚€_nonneg (hx : 0 < x) : 0 â‰¤ nF''â‚€ x  := Type1_nonneg (isType1_nF''â‚€) hx

/-!
  As a convenient corollary we find that nF'â‚€ is nonpositive.
-/

lemma nF'â‚€_nonpos (hx : 1 â‰¤ x) : nF'â‚€ x â‰¤ 0  := by
  have hâ‚ : -4 * nF'â‚€ x = nF''â‚ x := by unfold nF'â‚€ nF''â‚; ring
  have hâ‚‚ : 0 â‰¤ nF''â‚ x           := isType2_nF''â‚.2 hx
  linarith


/-!
### Continuity of F, F' and F'' at x = 1.
-/

lemma F_continuousAt_one : ContinuousAt F 1 := by
  convert ContinuousAt.lhopital_iterated 2
    [nFâ‚€, nFâ‚, nFâ‚‚] [dFâ‚€, dFâ‚, dFâ‚‚] rfl rfl ?_ ?_ ?_ ?_ ?_ ?_ ?_ using 1
  Â· unfold F nFâ‚‚ dFâ‚‚; congr! 2; norm_num
  Â· intro k _
    interval_cases k <;> (rw [Metric.eventually_nhds_iff]; use 1, Real.zero_lt_one; intro _ hy)
    Â· exact hasDerivAt_nFâ‚€ (pos_of_dist_one_lt_one hy)
    Â· exact hasDerivAt_nFâ‚ (pos_of_dist_one_lt_one hy)
  Â· intro k _
    interval_cases k <;> refine Filter.Eventually.of_forall ?_
    Â· exact (fun x â†¦ hasDerivAt_dFâ‚€)
    Â· exact (fun x â†¦ hasDerivAt_dFâ‚)
  Â· intro k _
    interval_cases k <;> norm_num [nFâ‚€, nFâ‚]
  Â· intro k _
    interval_cases k <;> norm_num [dFâ‚€, dFâ‚]
  Â· exact ContinuousAt_nFâ‚‚ (by norm_num)
  Â· exact ContinuousAt_dFâ‚‚
  Â· simp [dFâ‚‚]

lemma F'_continuousAt_one : ContinuousAt F' 1 := by
  convert ContinuousAt.lhopital_iterated 3
    [nF'â‚€, nF'â‚, nF'â‚‚, nF'â‚ƒ] [dF'â‚€, dF'â‚, dF'â‚‚, dF'â‚ƒ] rfl rfl ?_ ?_ ?_ ?_ ?_ ?_ ?_ using 1
  Â· unfold F' nF'â‚ƒ dF'â‚ƒ; congr! 2; norm_num
  Â· intro k _
    interval_cases k <;> (rw [Metric.eventually_nhds_iff]; use 1, Real.zero_lt_one; intro _ hy)
    Â· exact hasDerivAt_nF'â‚€ (pos_of_dist_one_lt_one hy)
    Â· exact hasDerivAt_nF'â‚ (pos_of_dist_one_lt_one hy)
    Â· exact hasDerivAt_nF'â‚‚ (pos_of_dist_one_lt_one hy)
  Â· intro k _
    interval_cases k <;> refine Filter.Eventually.of_forall ?_
    Â· exact (fun x â†¦ hasDerivAt_dF'â‚€)
    Â· exact (fun x â†¦ hasDerivAt_dF'â‚)
    Â· exact (fun x â†¦ hasDerivAt_dF'â‚‚)
  Â· intro k _
    interval_cases k <;> norm_num [nF'â‚€, nF'â‚, nF'â‚‚]
  Â· intro k _
    interval_cases k <;> norm_num [dF'â‚€, dF'â‚, dF'â‚‚]
  Â· exact ContinuousAt_nF'â‚ƒ (by norm_num)
  Â· exact ContinuousAt_dF'â‚ƒ
  Â· simp [dF'â‚ƒ]

lemma F''_continuousAt_one : ContinuousAt F'' 1 := by
  convert ContinuousAt.lhopital_iterated 4
    [nF''â‚€, nF''â‚, nF''â‚‚, nF''â‚ƒ, nF''â‚„] [dF''â‚€, dF''â‚, dF''â‚‚, dF''â‚ƒ, dF''â‚„]
      rfl rfl ?_ ?_ ?_ ?_ ?_ ?_ ?_ using 1
  Â· unfold F'' nF''â‚„ dF''â‚„; congr! 2; norm_num
  Â· intro k _
    interval_cases k <;> (rw [Metric.eventually_nhds_iff]; use 1, Real.zero_lt_one; intro _ hy)
    Â· exact hasDerivAt_nF''â‚€ (pos_of_dist_one_lt_one hy)
    Â· exact hasDerivAt_nF''â‚ (pos_of_dist_one_lt_one hy)
    Â· exact hasDerivAt_nF''â‚‚ (pos_of_dist_one_lt_one hy)
    Â· exact hasDerivAt_nF''â‚ƒ (pos_of_dist_one_lt_one hy)
  Â· intro k _
    interval_cases k <;> refine Filter.Eventually.of_forall ?_
    Â· exact (fun x â†¦ hasDerivAt_dF''â‚€)
    Â· exact (fun x â†¦ hasDerivAt_dF''â‚)
    Â· exact (fun x â†¦ hasDerivAt_dF''â‚‚)
    Â· exact (fun x â†¦ hasDerivAt_dF''â‚ƒ)
  Â· intro k _
    interval_cases k <;> norm_num [nF''â‚€, nF''â‚, nF''â‚‚, nF''â‚ƒ]
  Â· intro k _
    interval_cases k <;> norm_num [dF''â‚€, dF''â‚, dF''â‚‚, dF''â‚ƒ]
  Â· exact ContinuousAt_nF''â‚„ (by norm_num)
  Â· exact ContinuousAt_dF''â‚„
  Â· norm_num [dF''â‚„]


/-!
### Derivatives of F, F'.
-/

/-!
  First two auxilliary lemmas giving the derivatives on S.
-/

lemma hasDerivAt_F_on_S (hx : x âˆˆ S) : HasDerivAt F (F' x) x := by
  refine HasDerivAt.congr_on_open S_open hx ?_ F_on_S.symm
  convert HasDerivAt.div (hasDerivAt_nFâ‚€ hx.1) hasDerivAt_dFâ‚€ (dFâ‚€_ne_zero_on_S hx) using 1
  rw [F'_on_S hx]
  field_simp [dFâ‚€_ne_zero_on_S hx, dF'â‚€_ne_zero_on_S hx]
  unfold dFâ‚€ nFâ‚€ dF'â‚€ nF'â‚€ nFâ‚ dFâ‚
  ring

lemma hasDerivAt_F'_on_S (hx : x âˆˆ S) : HasDerivAt F' (F'' x) x := by
  refine HasDerivAt.congr_on_open S_open hx ?_ F'_on_S.symm
  convert HasDerivAt.div (hasDerivAt_nF'â‚€ hx.1) hasDerivAt_dF'â‚€ (dF'â‚€_ne_zero_on_S hx) using 1
  rw [F''_on_S hx]
  field_simp [dF'â‚€_ne_zero_on_S hx, dF''â‚€_ne_zero_on_S hx]
  unfold dF'â‚€ nF'â‚€ dF''â‚€ nF''â‚€ nF'â‚ dF'â‚
  field_simp [(ne_of_lt hx.1).symm]
  ring

/-!
  Then the derivatives on (0,âˆ).
-/

lemma hasDerivAt_F (hx : 0 < x) : HasDerivAt F (F' x) x := by
  by_cases hxS : x âˆˆ S
  Â· exact hasDerivAt_F_on_S hxS
  Â· rw [pos_not_in_S_eq_one hx hxS]
    exact HasDerivAt.extend_to_singularity (isOpen_Ioi (a := 0)) (by norm_num)
      (fun y hyn1 hy â†¦ hasDerivAt_F_on_S âŸ¨hy,hyn1âŸ©) (F_continuousAt_one) (F'_continuousAt_one)

lemma hasDerivAt_F' (hx : 0 < x) : HasDerivAt F' (F'' x) x := by
  by_cases hxS : x âˆˆ S
  Â· exact hasDerivAt_F'_on_S hxS
  Â· rw [pos_not_in_S_eq_one hx hxS]
    exact HasDerivAt.extend_to_singularity (isOpen_Ioi (a := 0)) (by norm_num)
      (fun y hyn1 hy â†¦ hasDerivAt_F'_on_S âŸ¨hy,hyn1âŸ©) (F'_continuousAt_one) (F''_continuousAt_one)

/-!
### Continuity of F, F'.
-/

lemma F_Continuous : Continuous F  := by
  rw [continuous_iff_continuousAt]
  intro x
  by_cases hx : x = 1
  Â· rw [hx]
    exact HasDerivAt.continuousAt (hasDerivAt_F (by norm_num))
  Â· rw [â†continuousWithinAt_iff_continuousAt (mem_nhds_iff.2 âŸ¨_, âŸ¨fun _ a â†¦ a, isOpen_ne, hxâŸ©âŸ©)]
    exact ContinuousWithinAt.congr (f := fun x â†¦ nFâ‚€ x / dFâ‚€ x) (
      ContinuousWithinAt.div (ContinuousAt.continuousWithinAt ContinuousAt_nFâ‚€)
        (ContinuousAt.continuousWithinAt ContinuousAt_dFâ‚€ ) (pow_ne_zero 2 (sub_ne_zero_of_ne hx)))
        (fun _ hy â†¦ by simp only [F, if_neg hy, nFâ‚€, dFâ‚€]) (by simp only [F, if_neg hx, nFâ‚€, dFâ‚€])

lemma F_ContinuousAt (hx : 0 < x) : ContinuousAt F x   := HasDerivAt.continuousAt (hasDerivAt_F hx)
lemma F'_ContinuousAt (hx : 0 < x) : ContinuousAt F' x := HasDerivAt.continuousAt (hasDerivAt_F' hx)

/-
### Convexity of F, nonpositivity of F' and F is at most 1.
-/

lemma F''_nonnegative (hx : 0 < x) : 0 â‰¤ F'' x := by
  Â· by_cases hxS : x âˆˆ S
    Â· rw [F''_on_S hxS]
      exact div_nonneg (nF''â‚€_nonneg hxS.1) (dF''â‚€_nonneg hxS.1)
    Â· rw [F'', pos_not_in_S_eq_one hx hxS]
      norm_num

lemma F_convex : ConvexOn â„ (Set.Ioi 0) F := by
  refine convexOn_of_hasDerivWithinAt2_nonneg (f := F) (f' := F') (f'' := F'') (convex_Ioi 0)
    (fun x hx â†¦ ContinuousAt.continuousWithinAt (F_ContinuousAt hx))
    ?_ ?_ ?_ <;> (intro x hx; rw [interior_Ioi] at hx;)
  Â· exact HasDerivAt.hasDerivWithinAt (hasDerivAt_F hx)
  Â· exact HasDerivAt.hasDerivWithinAt (hasDerivAt_F' hx)
  Â· exact F''_nonnegative hx

lemma F_convex_inequality {x y : â„} (hx : 0 â‰¤ x) (hy : 0 < y)
    : F x â‰¥ F y + F' y * (x - y) := by
  rcases (LE.le.lt_or_eq' hx) with hx | hx
  Â· exact ConvexOn.tangent_line_le F_convex hx hy (hasDerivAt_F hy)
  Â· rw [ge_iff_le, hx]
    refine ContinuousWithinAt.closure_le (s := Ioo 0 1)
      (f := fun x â†¦ F y + F' y * (x - y)) ?_ ?_ ?_ ?_
    Â· rw [closure_Ioo (by norm_num)]
      exact unitInterval.zero_mem
    Â· exact Continuous.continuousWithinAt (by continuity)
    Â· refine ContinuousWithinAt.congr ?_ (fun _ hy â†¦ F_on_S âŸ¨hy.1, (ne_of_gt hy.2).symmâŸ©) ?_
      Â· refine ContinuousWithinAt.div ?_ ?_ (by simp [dFâ‚€])
        Â· exact ContinuousAt.continuousWithinAt ContinuousAt_nFâ‚€
        Â· exact ContinuousAt.continuousWithinAt ContinuousAt_dFâ‚€
      Â· simp [F,nFâ‚€,dFâ‚€]
    Â· exact fun _ hx â†¦ ConvexOn.tangent_line_le F_convex hx.1 hy (hasDerivAt_F hy)

lemma F'_nonpositive (hx : 0 < x) : F' x â‰¤ 0 := by
  have h_mon : MonotoneOn F' (Ioi 0) := by
    refine monotoneOn_of_hasDerivWithinAt_nonneg (f' := F'') (convex_Ioi 0) ?_ ?_ ?_
    Â· exact fun y hy â†¦ ContinuousAt.continuousWithinAt (F'_ContinuousAt hy)
    Â· rw [interior_Ioi]
      exact fun y hy â†¦ HasDerivAt.hasDerivWithinAt (hasDerivAt_F' hy)
    Â· rw [interior_Ioi]
      exact fun y hy â†¦ F''_nonnegative hy
  rcases (le_or_gt x 1) with hx1 | hx1
  Â· exact le_trans (h_mon hx (by norm_num) hx1) (by norm_num [F'])
  Â· rw [F'_on_S âŸ¨by linarith,(ne_of_lt hx1).symmâŸ©]
    exact div_nonpos_of_nonpos_of_nonneg (nF'â‚€_nonpos (le_of_lt hx1)) (dF'â‚€_nonneg hx1)

lemma F_at_most_one (hx : 0 â‰¤ x) : 1 â‰¥ F x := by
  have h_mon : AntitoneOn F (Ici 0) := by
    apply antitoneOn_of_hasDerivWithinAt_nonpos (f' := F')
    Â· exact Continuous.continuousOn F_Continuous
    Â· rw [interior_Ici]
      exact fun y hy â†¦ HasDerivAt.hasDerivWithinAt (hasDerivAt_F hy)
    Â· rw [interior_Ici]
      exact fun y hy â†¦ F'_nonpositive hy
    Â· exact convex_Ici 0
  rw [â†(by simp[F] : F 0 = 1)]
  exact h_mon self_mem_Ici hx hx


/-
### Finally the differential equation that F satisfies.
-/

lemma F_diff_equation : (x - x^2) * F' x - (x + 1) * F x + 1 = 0 := by
  by_cases hx : x = 1
  Â· rw [hx, F_at_one, F'_at_one]; norm_num
  Â· rw [F_not_one hx, F'_not_one hx, â†mul_right_inj' (dFâ‚€_ne_zero hx),
        â†mul_right_inj' (dF'â‚€_ne_zero hx), mul_zero, mul_zero]
    field_simp [(dFâ‚€_ne_zero hx), (dF'â‚€_ne_zero hx)]
    unfold dFâ‚€ dF'â‚€ nFâ‚€ nF'â‚€
    ring
